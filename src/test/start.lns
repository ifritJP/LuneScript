_lune_control use_macro_special_var;

subfile use test.startsub;

import test.macro as Macro;
import test.simple;
import lune.base.Writer; // skip-cross
//import test.Sub2;
import test.Sub2 as SUB2;
import test.Sub6;
import test.Class1; // skip-lua51
import test.Class2; // skip-lua51
import test.Class3; // skip-lua51
import test.Class4; // skip-lua51
import test.mapping;
import test.test2.test2.test as TESTSUB;
import.l test.Sub8;
import.d test.Sub10;
import test.Sub11;

macro _print( symbol2:stat, format:str! ) {
   {
      let work = unwrap format default "%s";
   }
   print( string.format( "macro %s " .. ,,work, ,,,,symbol2, ,,symbol2 ));
}

let mut classA = new simple.ClassA();
print( classA.func( 0 ) );
print( classA.func( 0 ) );
print( classA.sub() );

let classB = new simple.ClassB( 123 );
print( "classB.func()", classB.func() );
print( "classB.sub()", classB.sub() );
let hoge = classB.func2();
print( "Hoge.func()", hoge.func() );
SUB2.sub2val = 10;
print( "Hoge.func()", hoge.func() );

let func3ret = classB.func3() ;
foreach val in func3ret {
   print( val );
}

print( "12%d" ( 10 ) );

{
   let val = 1;
   print( "%g" (val@@real));
}

print( "op", type( {} ),  1 and "2" and 3.0 and type( {} ) );
print( "op", 3.0, 1 and "2" and 3.0 or  {} );
print( "op", "2", 1 and "2" or  3.0 and {} );
print( "op", "2", 1 and "2" or  3.0 or  {} );
print( "op", 1,   1 or  "2" and 3.0 and {} );
print( "op", 1,   1 or  "2" and 3.0 or  {} );
print( "op", 1,   1 or  "2" or  3.0 and {} );
print( "op", 1,   1 or  "2" or  3.0 or  {} );

for index = 1, 4 {
   switch index {
      case 1, 2 {       
         print( "hoge", index );
      }   
      case 3 {
         print( "foo", index );
      }   
      default {
         print( "bar", index );
      }   
   }   
}

fn func__0( val: int! ): int {
   let! val2 = val { return 0; };
   
   return val2 + 2;
}

print( 3, func__0( 1 ) );
print( 0, func__0( nil ) );

macro _test() {
   print( "macro hoge" );
}
_test();


macro _test2( val:int, funcxx:sym ) {
   {
      fn func(val2:int):str {
         return "mfunc%d" (val2);
      }
      let message = "hello %d %s" ( val, ,,,,funcxx );
      let stat1 = `{ print( "macro stat" ); };
      let stat2 = `{
         for index = 1, 10 {
            print( "hoge %d" ( index ) );
         }
      };
      let mut stat3:stat[] = [];
      for index = 1, 4 {
         stat3.insert( `{ print( "foo %d" ( ,,index ) ); } );
      }
      let symbol1 = ,,,func( 1 );
      let symbol2 = ,,,"mfunc%d" (1);
      let stat4 = `{
         ,,symbol1( "100" );
         ,,symbol2( ,,func( 2 ) );
         ,,,func(1)~~( ,,,,symbol1 );
      };
   }
   print( ,,message );
   print( "--funcxx--" );
   ,,funcxx( "macro test2" );
   print( "--stat--" );
   ,,stat1;
   print( "--stat2--" );
   ,,stat2;
   print( "--stat3--" );
   ,,stat3;
   print( "--symbol1--" );
   ,,symbol1( "10" );
   print( "--stat4--" );
   ,,stat4;
}

fn mfunc1( val: str ) {
   print( "mfunc1", val );
}


_test2( 1, print );

macro _classtest( macroVal: int, fieldInfoList: List<Map<str,str>> ) {
   {
      let name = ,,,"classTest%d" (macroVal);
      let mut field:stat[] = [];

      foreach fieldInfo in fieldInfoList {
         foreach typeName, varName in fieldInfo {
            field.insert( `{
               pri let ,,,varName : ,,,typeName { pub };
            });
         }
      }
   }
   class ,,name {
      ,,field;
      pub fn func(): int {
         return ,,macroVal;
      }
   }
}

_classtest( 1, [ { "val": "int" }, { "val2": "str" } ] ) ;
_classtest( 2, [] );
_classtest( 3, [] );

{
   let classObj = new classTest1( 1000, "2000" );
   print( classObj.func(), classObj.get_val(), classObj.get_val2() );
}
{
   let classObj = new classTest2();
   print( classObj.func() );
}
{
   let classObj = new classTest3();
   print( classObj.func() );
}


macro _stattest( val: stat ) {
   ,,val;
}

_stattest(
   `{
      print( "1" );
      print( "2" );
   } );

macro _symboltest( symbol: sym, val:stem ) {
   print( __line__, ,,symbol( ,,val ) );
}

_symboltest( string.format, "test\n" );



class GetTest {
   let val: int { pub };
   pub fn __init() {
      self.val = 100;
   }
}
print( (new GetTest()).$val );

class GetTest2 {
   let val: int { pub };
   let mut val2: int { non, pub };
   pub fn __init() {
      self.val = 200;
      self.val2 = 1;
   }
   pub fn getVal2(): int {
      return self.val2;
   }
}

{
   let mut test2 = new GetTest2();
   test2.set_val2( 1 );
   print( test2.$val, test2.getVal2() );
}

{
   fn TestRetDDD(): ... {
      return 1,2,3;
   }   
   let! ddd1, ddd2, ddd3 = TestRetDDD() {
      error( "fail" );
   };
   print( "2 = %d, 3 = %d, 4 = %d"
          (ddd1@@int + 1, ddd2@@int + 1, ddd3@@int + 1 ) );
}

{
   let nilableInt1: int! = nil;
   let! unwrapInt1, unwrapInt2: int = nilableInt1, nil {
      unwrapInt1 = 123; unwrapInt2 = unwrap _unwrapInt2 default -456; }
   then {
      print( "NG" );
   };
   print( "123 = %d, -456 = %d" ( unwrapInt1, unwrapInt2 ) );
}

{
   let nilableInt1: int! = 567;
   let! unwrapInt1, unwrapInt2: int = nilableInt1, 890 {
      unwrapInt1 = 123;
   }; 
   print( "567 = %d, 890 = %d" (unwrapInt1, unwrapInt2 ) );
}

{
   let hoge1: int = 1;
   let hoge2: int = 2;
   let val3:int! = 3;
   let val4:int! = 4;
   unwrap! hoge1, hoge2 = val3, val4 {
      print( "NG" );
   }
   then {
      print( "3 = %d, 4 = %d" ( hoge1, hoge2 ) );
   };
   print( "3 = %d, 4 = %d" ( hoge1, hoge2 ) );
}

// {
//    let mut hoge1: int = 1;
//    let mut hoge2: int = 2;
//    let val3:int! = 3;
//    let val4:int! = 4;
//    sync! hoge1, hoge2 = val3, val4 {
//       error( "NG" );
//    }
//    then {
//       print( "3 = %d, 4 = %d" ( hoge1, hoge2 ) );
//    }
//    do {
//       print( "3 = %d, 4 = %d" ( hoge1, hoge2 ) );
//       hoge1 = hoge1 * 10;
//       hoge2 = hoge2 * 10;
//    };
//    print( "30 = %d, 40 = %d" ( hoge1, hoge2 ) );
// }



{
   let nilableInt1: int! = null;
   if! nilableInt1 {
      print( _exp, "not nil" );
   }
   else {
      print( "nil" );
   }
}

{
   let nilableInt1: int! = 1;
   if! nilableInt1 {
      print( _exp, "not nil" );
   }
   else {
      print( "nil" );
   }
}

{
   let test: int! = nil;
}

{
   let mut val:int! = nil;
   if! let val1, val2:int = 1, val {
      print("NG");
   }
   else {
      print( "OK" );
   }
   val = 2;
   if! let val1, val2:int = 1, val {
      print( "OK", val1, val2 );
   }
   else {
      print("NG");
   }
}

fn testret() : int! {
   return nil;
}

{
   let mut arry: int[] = [];
   arry.insert( 1 );
   foreach val, index in arry {
      print( index, val );
   }
   arry.remove();
}

{
   print( 10 / 3, 10 / 3.0, (10 / 3.0)@@int );
}

{
   foreach val in [ 1, 2, 3 ] {
      let val2:int = val;
   }
}   

class ClassAA {
   pri static let val:int { pub };

   __init {
      ClassAA.val = 1;
   }
   
   pub static fn func() {
      print( "val = %s" (ClassAA.val) ) ;
   }
}

ClassAA.func();
_print( `{ ClassAA.$val } );
{
   let classAA = new ClassAA();
}

SUB2.External.func1( 100 );
SUB2.External.func2( 10 );
SUB2.External.func2( 20 );
_print( `{ SUB2.External.val } );

{
   let foo = {
      "bar": {
         "baz": 1,
         "hoge": [ 'a', 'b', 'c' ],
         "sage": "xyz",
      }
   };

   _print( `{ foo.bar$.baz }, "%s" );
   _print( `{ foo.ba$.baz }, "%s" );
   _print( `{ foo.bar$.hoge$[1] }, "%s" );
   _print( `{ foo.ba$.hoge$[1] }, "%s" );
   _print( `{ foo.bar$.sage$[1] }, "%c" );
   _print( `{ foo.ba$.sage$[1] }, "%s" );
}

{
   class Test {
      let val:int;
      pub fn func(): int {
         return self.val;
      }
   }
   let test:Test! = new Test( 1 );
   _print( `{ test$.func$() } );
   let test2:Test! = nil;
   _print( `{ test2$.func$() } );
   let mut funcnil:form! = fn( param:stem! ):int { print( param ); return 1; };
   _print( `{ funcnil$( nil ) } );
   funcnil = nil;
   _print( `{ funcnil$( 1 ) } );
}


{
   interface IF {
      pub fn func(): int;
   }
   class TestCC extend (IF) {
      pub fn func(): int {
         return 1;
      }
   }
   let test:IF = new TestCC();
   _print( `{ test.func() } );
}

{
   let mut val1, mut val2 = 1, 2;
   val1, val2 = 3, 4;
   _print( `{ val1 } );
   _print( `{ val2 } );
}

{
   class TestDD {
      pri let val:int { pub };
      pub fn func1(): int {
         return 1 + self.val;
      }
      pub fn func2(): int {
         return 2 + self.val;
      }
   }
   class TestEE {
      pri let testDD: TestDD;
      pub fn __init() {
         self.testDD = new TestDD( 100 );
      }
      pub fn func1(): int {
         return 3;
      }
      advertise testDD;
   }
   let testEE = new TestEE();
   _print( `{ testEE.func1() } );
   _print( `{ testEE.func2() } );
}

{
   let mut jsonWriter = new Writer.JSON( io.stdout ); // skip-cross
   jsonWriter.startParent( "hoge", true ); // skip-cross
   jsonWriter.write( "foo", "bar" ); // skip-cross
   jsonWriter.endElement(); // skip-cross
   jsonWriter.fin(); // skip-cross
}

{
   let val = 0xff1;
   _print( `{ val } );
}


{
   enum enumTest {
      val1,
      val2,
      val3 = val2 + 10 - 1 * 2 / 3,
      val4
   }
   _print( `{ enumTest.val4 } );
   let mut val2 = 0;
   val2 = enumTest.val1;
   let val3:str = enumTest.val1.$_txt;

   foreach val in enumTest.$_allList {
      _print( `{ val.$_txt } );
   }
   

   enum enumTest2 {
      val1 = 1.0,
      val2,
      val3 = val2 + 10 - 1 * 2 / 3,
      val4
   }
   _print( `{ enumTest2.val4 } );
   _print( `{ val3 } );
}

{
   let mut val = { "abc": 1 };
   //val.abc = 1;
}

{
   let mut val = { "abc": 1 };
   print( val.abc );
   val.abc = 2;
   val.efg = 3;
   val[ "zzz" ] = 4;
   print( val[ "ab" .. "c" ], val.efg, val.zzz );
}

interface IFTEST {
   pub fn sub();
}

class TEST2 extend (IFTEST) {
   pub fn sub() {
      print( "TEST2.sub()" );
   }
}


class TEST extend (IFTEST) {
   let iftest:IFTEST;

   advertise iftest;
}

{
   let test = new TEST( new TEST2() );
   test.sub();
}

enum TESTENUM {
   val1,
   val2,
   val3,
}

enum TESTENUM2 {
   val1 = 100,
   val2,
   val3,
}

{
   let mut val = TESTENUM.val1;
   val = .val1;
   print( val );
   val = .val2;
   print( val );

   switch ( val ) {
      case .val1, .val2 {
         print( "ok" );
      }  
      default {
         print( "ng" );
      }  
   }

   fn testenum( enum1:TESTENUM, enum2:TESTENUM2 ) {
      print( "testenum", enum1, enum2 );
   }
   testenum( .val2, .val2 );
}

class TestDest {
   let mut val:int;
   pub fn __init( val:int ) {
      self.val = val;
      fn inner() {
         print( "%s" (__func__) );
      }
      print( "%s" (__func__) );
   }
   fn __free() {                                        // skip-lua51
      self.val = self.val * 10;                         // skip-lua51
      print( "__free(): %d %s" (self.val, __func__) );  // skip-lua51
   }                                                    // skip-lua51
}
new TestDest( 100 );
collectgarbage();


module GlueTest require 'test.glueTest' glue 'hoge_' {
   pub static fn create(val:int): GlueTest;
   pub static fn func1( val:int, txt:str, list:&List<int> ) : int;
   pub fn func2( val:int, val2:int!, val3:real!, val4:str! ): str;
   //pub fn func2( val:int ): str;
}

{
   GlueTest.func1( 10, "abc", [] );
   let age = GlueTest.create( 100 );
   age.func2( 2 );
   age.func2( 2, 3, 4.0, "xyz" );
}


_print( `{ "%X" (0x1 & 0x3 ) } ); // skip-lua51
_print( `{ "%X" (0x1 | 0x2 ) } ); // skip-lua51
_print( `{ "%X" (0x1 ~ 0x3 ) } ); // skip-lua51
_print( `{ "%X" (0x10 |>> 2 ) } ); // skip-lua51
_print( `{ "%X" (0x1 |<< 2 ) } ); // skip-lua51
_print( `{ "%X" (~2 ) } ); // skip-lua51

_print( `{ __mod__ } );
_print( `{ __line__ } );

mapping.test();

{
   import test.funcSym;
   funcSym.main();
}

class SubIF extend SUB2.IF {
   pub fn __init() {
      super( 1 );
   }
   pub fn sub( val: SUB2.enumTestSub ) {
      print( val );
   }
}
{
   let sub = new SubIF();
   sub.sub( .val1 );
}

Macro._section();
macro _test3() {
   {
      let mut val3:_List<stat> = [];
      let mut fileObj = unwrap io.open( "test/data.lua" );
      let txt = unwrap fileObj.read( "*a" );
      __luago {
         let val = unwrap _load( txt )$();
         let val2 = unwrap val.a;
         forsort val4, key in val2@@Map<str,stem> {
            val3.insert( `{ ,,,key = ,,val4, } );
         }
      }
   }
   enum MacroTest {
      ,,val3
   }
}
_test3();
print( MacroTest.A234, MacroTest.B234 );


macro _test4( txt:str ) {
   {
      let val = _lnsLoad( "aa", txt ).val;
   }
   print( ,,val, type( ,,val ) );
}

_test4( ```
pub let val = 100;
``` );


{
   fn sub():int! {
      return 0;
   }  
   fn func__c( val:int!, val2:int! ): int {
      let val3 = 1;
      when! val, val2 {
         return val + val2;
      }
      return 0;
   }
   _print( `{ func__c( 1, 2 ) } );
   _print( `{ func__c( nil, 2 ) } );
   _print( `{ func__c( 1, nil ) } );
}

{
   macro _test5() {
      {  
         let mut val = `{};
         val = `{ print( "test" ); };
      }  
      ,,val;
   }  
   _test5();
}

{
   fn sub1( val:int ):int {
      return 10 + val;
   }  
   fn func( testfunc:SUB2.form1, val:int ) {
      _print( `{ testfunc( val ) + 2 } );
   }  
   func( sub1, 3 );
}
Macro._section();

{
   fn ite( param:int, prev:stem! ) : int!, str {
      let mut prevInt:int = 0;
      if prev == param {
         return nil, "";
      }  
      when! prev {
         prevInt = prev@@int;
      }  
      else {
         prevInt = 0;
      }  
      let next = prevInt + 1;
      return next, string.format( "%d", next );
   }  

   form iteForm( param:int, prev:stem! ) : int!, str;

   fn testIte( max:int ): iteForm, int, int! {
      return ite, max, nil;
   }  

   apply value1, value2 of testIte( 10 ) {
      print( string.format( '%d "%s"',  value1 + 0, value2.sub( 1 ) ) );
   }
}
Macro._section();

{
   fn func( val:int, ... ) : int, int, ... {
      let argList = [ ... ];
      let mut total = val;
      foreach arg in argList {
         when! arg {
            total = total + arg@@int;
         }  
      }  
      return total, val, ...;
   }  

   print( func( 1, 2, 3, 4 ) ); // 10 1 2 3 4 
}

{
   let list = [ 1, 2, 3 ];
   print( list.unpack() );
   let array = [@ 1, 2, 3 ];
   print( array.unpack() );
}

{
   fn sub( algeTest:SUB2.AlgeTest ) {
      match algeTest {
         case .Val1 {
            print( algeTest.$_txt, 1 );
         }  
         case .Val2 {
            print( algeTest.$_txt, 2 );
         }  
         case .Val3( x ) {
            print( algeTest.$_txt, x + 10 );
         }  
         case .Val4( x, y, z ) {
            print( algeTest.$_txt, x + 10, y .. "X" );
         }  
         case .Val5( x ) {
            print( algeTest.$_txt );
            sub( x );
         }
      }
      if algeTest == .Val1 {
         print( __line__, "Val1" );
      }
   }

   sub( .Val1 );
   sub( .Val2 );
   sub( .Val3( 3 ) );
   sub( .Val4( 4, "abc", new SUB2.Hoge2( 1 ) ) );
   sub( .Val5( .Val5( .Val3( 5 ) ) ) );
}

Macro._section();

class Test3 {
   pri let mut val:int {pub};
   _lune_control default__init;
   pub static fn create(): Test3 {
      return new Test3( 1 );
   }
}
{
   let test3 = Test3.create();
   print( test3.$val );
}

SUB2._MacroTest2( "abc", 123 );
SUB2._MacroTest3( 123 );
SUB2._test( .val2 );


{
   foreach val in [ SUB2.DDDfunc( 1, 2, 3 ) ] {
      print( val + 10 );
   }
   foreach val in [ SUB2.DDDfunc2( "1", nil, "3" ) ] {
      when! val {
         print( "abc" .. val );
      }
   }
}

Macro._section();

{
   fn func( val:int ):int!, int! {
      switch val {
         case 0 {
            return 1, 2;
         }  
         case 1 {
            return 1##;
         }  
         case 2 {
            return ##;
         }  
      }  
      return 1, 2;
   }
   print( func( 0 ) );
   print( func( 1 ) );
   print( func( 2 ) );
}


{
   let mut list = [ 1, 4, 5, 3, 2 ];
   list.sort( fn ( val1:int, val2:int ) : bool { return val1 > val2; } );
   foreach val in list {
      print( val );
   }
   list.sort(##);
   foreach val in list {
      print( val );
   }  
}
{
   let set = (@ "1", "2", "3", "4" );
   forsort val in set {
      print( val .. "a" );
   }
}
{
   let set = (@ 1, 2, 3, 4 );
   let mut total = 0;
   foreach val in set {
      total = total + val;
   }
   print( total );
   forsort val in set {
      print( val + 1 );
   }
}
Macro._section();
{
   let mut set1 = (@  1, 2, 3 );
   let set2 = (@  2, 3, 4 );
   macro _dumpSet( setExp:stat ) {
      {  
      }  
      {  
         print( "===== %s" (,,,,setExp) );
         let set = ,,setExp;
         foreach val in set {
            print( val + 10 );
         }  
      }
   }  

   _dumpSet( `{ set1.clone().or(set2) } );
   _dumpSet( `{ set1.clone().and(set2) } );
   _dumpSet( `{ set1.clone().sub(set2) } );
   print( "len:", set1.len() );
}
{
   let mut set:Set<int> = (@);
   set.add( 1 + 2 );
   foreach val in set {
      print( val );
   }
   if set.has( 3 ) {
      print( "exist 3" );
   }
   if not set.has( 1 ) {
      print( "not exist 1" );
   }
   set.del( 3 );
   foreach val in set {
      print( val );
   }
}
class SetTest extend (Mapping) {
   let set:Set<int>;
   pub fn func() {
      foreach val in self.set {
         print( val );
      }
   }
}

{
   let setTest = new SetTest( (@ 1, 2, 3 ) );
   let mapObj = setTest._toMap();
   if! let setTest2 = SetTest._fromMap( mapObj ) {
      setTest2.func();
   }
}

Macro._section();
{
   fn func<T>( val:Map<int,T> ): T! {
      return val[ 1 ];
   }  
   print( unwrap func( { 1:"123" } ) .. "abc" );
}


{
   let test1 = new SUB2.TestGen<int>(1);
   test1.func( 10 );
   test1.func( nil );
   test1.func2( 100 );
   let test2 = new SUB2.TestGen<str>("a");
   // SUB2.func( test2 );  // error

   let mut test3 = new SUB2.TestGen<int>(10);
   SUB2.func( test3 );

   let test4 = new SUB2.TestGen<real>(10.0);
   // SUB2.func( test4 );  // error
}


class GenTest {
}
class GenSub1 extend GenTest {
}
class GenSub2 extend GenTest {
}
Macro._section();

{
   fn func(test3:&SUB2.TestGen<GenTest>): &GenTest {
      return test3.$val;
   }  

   let test5 = new SUB2.TestGen<GenTest>(new GenTest());
   func( test5 );

   // let test6 = new SUB2.TestGen<GenSub1>(new GenSub1());
   // func( test6 );
}

// func( test3 );  // error

abstract class SuperG<T> {
   pub abstract fn func( val:T );
}

class TestGenSub extend SuperG<int> {
   pub override fn func( val:int ) {
      print( val );
   }
}

{
   let testsub = new TestGenSub();
   testsub.func( 10 );
   //testsub.func( "abc" );
}

class TestGenSub2<T> extend SuperG<T> {
   pub override fn func( val:T ) {
      print( val );
   }
}

{
   let testsub = new TestGenSub2<int>();
   testsub.func( 10 );
   //testsub.func( "abc" );
}
{
   let testsub = new TestGenSub2<str>();
   //testsub.func( 10 );
   testsub.func( "abc" );
}

Macro._section();
{
   let val:stem = { 1:"OK" };
   if! let val2 = val[1] {
      print( val2 );
   }  
}
{
   fn func3( val2:&Nilable<int> ) {
      if! val2.val() {
         print( __func__, _exp + 1 );
      }  
      print( __func__, val2.val() );
   }  

   let val1:Nilable = 1;
   let val2:Nilable<int> = nil;

   func3( val1 );
   func3( nil );
}

{
   print( SUB2.Boxfunc( nil ) );
   print( SUB2.Boxfunc( 1 ) );
   print( SUB2.Boxfunc( SUB2.Boxfunc2( 10 ) ) );
   print( SUB2.Boxfunc( SUB2.Boxfunc2( SUB2.Boxfunc3( 20 ) ) ) );
   print( SUB2.Boxfunc( SUB2.Boxfunc2( SUB2.Boxfunc3( nil ) ) ) );
}

// {
//    fn func__c<T>( val:Nilable<T> ): Nilable<T> {
//       return val;
//    }  

//    print( func__c( 1 ).val() );
//    print( func__c( 1.0 ).val() );
//    print( func__c( "str" ).val() );
// }

macro _dump( symbol:stat ) {
   print( "%s:%d> %s" (__mod__, __line__, ,,symbol ) );
}

class MapTest<T:(Mapping)> extend (Mapping) {
   let testval:T {pub};
}

{
   let test = new MapTest( 10 );
   let map = test._toMap();
   if! let test2 = MapTest<int>._fromMap( map ) {
      _dump( `{ test2.$testval } );
   }  
   else {
      print( "NG" );
   }  
}
Macro._section();

{
   let test = new MapTest( [ 1, 2, 3 ] );
   let map = test._toMap();
   if! let test2 = MapTest<List<int>>._fromMap( map ) {
      foreach val in test2.$testval {
         _dump( `{ val } );
      }
   }  
   else {
      print( "NG" );
   }  
}

{
   let test = new MapTest( new SUB2.Val( 1 ) );
   let map = test._toMap();
   if! let test2 = MapTest<SUB2.Val<int>>._fromMap( map ) {
      _dump( `{ test2.$testval.$valval } );
   }  
   else {
      print( "NG" );
   }
}

{
   let test = new MapTest( new SUB2.Val( "abc" ) );
   let map = test._toMap();
   if! let test2 = MapTest<SUB2.Val<str>>._fromMap( map ) {
      _dump( `{ test2.$testval.$valval } );
   }  
   else {
      print( "NG" );
   }  
}

{
   let test = new MapTest( new SUB2.Val( new SUB2.Val( "abc" ) ) );
   let map = test._toMap();
   if! let test2 = MapTest<SUB2.Val<SUB2.Val<str>>>._fromMap( map ) {
      _dump( `{ test2.$testval.$valval.$valval } );
   }  
   else {
      print( "NG" );
   }  
}

Macro._section();
{
   fn func( val:stem ) {
      if! val@@@int {
         print( "int:%d" ( _exp ) );
      }  
      if! val@@@real {
         print( "real:%g" ( _exp ) );
      }  
      if! val@@@str {
         print( "str:%s" ( _exp ) );
      }  
   }  
   func( 1 );
   func( 2.5 );
   func( "abc" );
 }

macro _Test6( name:sym ) {
   pub fn ,,name () {
      print( __line__, __func__ );
   }
}

class TestGG {
   _Test6( func1 );
   _Test6( func2 );
}  
{
   let test = new TestGG();
   test.func1();
   test.func2();
}

class Test2 {
   pro let val:int!;
}
class Sub2 extend Test2 {
   let val2:int;

   _lune_control default__init_old;
   
   pub fn func() {
      print( __line__, self.val, self.val2 );
   }
}

{
   let sub2 = new Sub2( 2 );
   sub2.func();
}

Macro._section();

macro _Hoge( val:str )
{
   {
      let mut stat0:List<stat> = [];
      stat0.insert( `{ case 0 { return "a"; } } );
      stat0.insert( `{ default { return "b"; } } );
      let name = "%sfunc" (val);
      let stat1 = `{
         fn ,,,name ~~(val:int):str {
            switch val {
               ,,stat0
            }
         }
      };
   }
   ,,stat1;
}

{
   _Hoge( "Test" );

   print( Testfunc( 0 ), Testfunc( 1 ) );
}

Macro._section();

class Test4 {
   pub let val:int {pub};
}
{
   fn func4() : Test4! {
      return nil;
   }
   let val:Test4! = nil;
   _print( `{ val$.$val } );
   _print( `{ func4()$.$val } );
}
{
   fn func4() : Test4! {
      return new Test4(2);
   }
   let val = new Test4( 1 );
   _print( `{ val.$val } );
   _print( `{ func4()$.$val } );
}
{
   fn func(val:stem!): int! {
      return val@@@int;
   }
   print( func( 1 ), func( nil ) );
}
{
   print( _kind( "hoge" ), _kind( 1 ), _kind( 1.0 ), _kind( [] ), _kind( {} ) );
}
Macro._section();
{
   fn func() {
      fn func2() {
         class Test_1 {
            let val:int;
            pub fn func2( val2:int ) {
               print( __func__, val2 );
            }
         }
         pub fn Test_1.func( val2:int ) {
            print( self.val, val2 );
         }
         
         let test = new Test_1( 1 );
         test.func( 1 );
         test.func2( 2 );
      }

      func2();
   }

   func();
}
Macro._section();
{
   enum Test_enum {
      Foo,
      Bar,
      Baz,
   }
   let val:Test_enum = .Bar;
   print( val.$_txt );
}
{
   class Test_c {
      let val:int;
   }
   class Sub extend Test_c {
      // 親クラスのコンストラクタに引数があり、
      // 継承しているクラスにメンバがない場合に、エラーする不具合があった。
   }
}
{
   fn func( val:int! ) {
      foreach item, index in [1,2] {
         if index == 1 {
            let! castType = val {
               castType = item;
            };
            print( castType );
         }
         else {
            let! castType = val {
               castType = item;
            };
            print( castType );
         }
      }
   }
   func( nil );
   func( 10 );
}
{
   fn func(): int, int, int {
      return 1, 2, 3;
   }
   fn sub2( val1:int, ... ) {
      print( val1, ... );
   }
   sub2( func()** );
}
{
   let func = fn () {
      print( "hoge" );
   }
   func();
}

{
   fn func( test:SUB2.Test_class ) {
      test.func();
   }

   func( new SUB2.Sub_class( ) );
}
print( "\\''\'\"\\.\\" );
print( '\\""\"\'\\.\\' );
{
   Macro._section();
   SUB2.testPrint();
   {
      import test.Sub2 as sub;
      sub.testPrint();
      {
         import test.Sub2 as subsub;
         subsub.testPrint();
         sub.testPrint();
      }
      sub.testPrint();
   }
   SUB2.testPrint();
}
{
   Macro._section();

   class Test_c<T> {
      let list:_List<T> {pub};
   }

   let test = new Test_c( [ 1, 2, 3, "abc" ] );
   foreach val in test.$list {
      print( val );
   }
}
{
   Macro._section();
   let test = SUB2.test3;
   test.func();
}
{
   Macro._section();
   print( SUB2._Test() );
}
{
   Macro._section();
   fn func():int,real,bool,str,int! {
      return 1,2.5,false,"abc",nil;
   }
   print( "hoge:%d,%g,%s,%s,%s" (func()) ); // skip-lua51
}
{
   Macro._section();
   interface IF {
   }
   class Super_c {
   }
   class Test_c extend Super_c (IF) {
   }

   fn func( val:stem ) {
      if! val@@@int {
         print( "int:%d" ( _exp ) );
      }
      if! val@@@real {
         print( "real:%g" ( _exp ) );
      }
      if! val@@@str {
         print( "str:%s" ( _exp ) );
      }
      if! val@@@Test_c {
         print( "class" );
      }
      if! val@@@IF {
         print( "IF" );
      }
   }
   func( 1 );
   func( 1.0 );
   func( "abc" );
   func( new Test_c() );
   func( new Test_c() );
}
{
   Macro._section();
   macro _getTxt( val:__exp ):int {
      {}
      (,,val + 1)
   }
   print( _getTxt( 10 ) );
   print();
}
{
   Macro._section();
   class Test_c {
      pub let mut val:int;
   }
   class Hoge<T:Test_c> {
      let mut test:T {pub};

      pub fn __init( test:T ) {
         self.test = test;
      }
      pri fn current() mut : T {
         return self.test;
      }
      pub fn set(val:int) mut {
         let mut info = self.current();
         info.val = val;
      }
   }
   let mut hoge2 = new Hoge( new Test_c( 1 ) );
   print( hoge2.$test.val );
   hoge2.set( 100 );
   print( hoge2.$test.val );
}
{
   Macro._section();
   macro _test1( val:sym ) {
      print( "hoge", __line__, ,,val );
   }

   macro _test_2() {
      _test1( work );
   }


   let work = 1;
   _test_2();
   _test1( work);
}
{
   Macro._section();
   macro _test1( val:__exp ) {
      print( "hoge", __line__, ,,val );
   }

   macro _test1_2() {
      _test1( 1 );
   }

   _test1_2();
}
{
   Macro._section();
   macro _test1( msg:str ) {
      print( "hoge:" .. ,,msg );
   }

   macro _hoge( msg2:str ) {
      _test1( "foo:" .. ,,msg2 );
      _test1( "foo:" .. ,,msg2 );
   }

   _hoge( "abc" );

}
{
   Macro._section();
   macro _test1( msg:str ) {
      print( "foo: %s" ( ,,msg ) );
   }
   macro _hoge( msg2:str ) {
      _test1( "hoge: %s" (,,msg2 ) );
   }

   _hoge( "abc" );
}

Macro._section();
class Test_ccc {
   pri static let mut val:int {pub,pub};
   pri static let mut val2:str {pub,pub};
   pri let mut val3:int {pub,pub};
   pri let mut val4:str {pub,pub};

   __init {
      Test_ccc.val = 0;
      Test_ccc.val2 = "abc";
   }
}
{
   print( Test_ccc.$val, Test_ccc.$val2 );
   Test_ccc.set_val( 100 );
   Test_ccc.set_val2( "xyz" );
   print( Test_ccc.$val, Test_ccc.$val2 );

   let mut test = new Test_ccc( 10, "ABC" );
   print( test.$val3, test.$val4 );
   test.set_val3( 1000 );
   test.set_val4( "XYZ" );
   print( test.$val3, test.$val4 );
}
{
   Macro._section();
   class Hoge {
      let val:str;
      pub fn get_val(): str {
         return self.val;
      }
   }
   class Sub extend Hoge {
   }
   class Test_c extend Hoge {
      let sub:Sub;

      // この advertise で、 get_val は super.get_val() ではなく、sub.get_val() になる。
      advertise sub; 
   }
   {
      let test = new Test_c( "xyz", new Sub( "abc" ) );
      print( test.$val );
   }
}
{
   Macro._section();
   macro _DefOption( mode:str ) {
      {
         let name;
         name = ,,,mode;
      }
      class ,,name {
      }
   }

   _DefOption( "HogeAA" );
   let hogeAA = new HogeAA();
}
{
   Macro._section();
   class Hoge {
   }
   macro _DefOption2( work:str ) {
      class HogeA {
         let val:,,,work;
      }
   }
   _DefOption2( "&Hoge" );
   let hogeAA = new HogeA( new Hoge() );
}
{
   Macro._section();
   macro _test__var0( name:str, val:int ) {
      {
         __var[ name ] = val;
      }
   }
   macro _test__var1() {
      {
         let val;
         if! let work = __var[ "hoge" ] {
            val = work@@int;
         }
         else {
            val = 10;
         }
      }
      print( "%s" (,,val) );
   }
   _test__var0( "hogea", 1 );
   _test__var1();
   _test__var0( "hoge", 1 );
   _test__var1();
}
{
   Macro._section();
   interface Imp {
      pub fn func();
   }
   class Super2 extend (Imp) {
      pub fn func() {
         print( __func__ );
      }
   }
   class Sub extend (Imp) {
      pub fn func() {
         print( __func__ );
      }
   }
   let list = [ new Super2(), new Sub() ];
   foreach item in list {
      item.func();
   }
}
{
   Macro._section();
   macro _test5( val:__exp ) {
      let test = ,,val;
   }

   _test5( new SUB2.Test() );

   macro _hoge( val:__exp ) {
      let foo = ,,val;
   }

   _hoge( fn ( aa:SUB2.Test ) {} );
}
{
   Macro._section();
   form TestForm( map:Map<int,str>! );
   fn func( test:TestForm ) {
      print( __func__ );
   }

   macro _hoge( val:__exp ) {
      func( ,,val );
   }
   _hoge( fn( map:Map<int,str>! ){} );

}
{
   Macro._section();
   macro _Test( val:int ) {
      {
         let defstat = `{
            fn ,,,"func%d"(val)~~():int {
               return ,,val;
            }
         };
      }
      ,,defstat;
   }

   _Test( 1 );
   _Test( 10 );

   print( func1(), func10() ); // 1  10
}
{
   Macro._section();
   macro _hoge( val:__exp ) : __exp {
      "%d" (,,val)
   }

   print( _hoge( 1 ) );
   
}
{
   macro _hoge() : __exps {
      sub()
   }
   fn sub(): int, int {
      return 1, 2;
   }

   print( _hoge()** );
}
{
   Macro._section();
   macro _hoge(arg:__exp) {
      let foo = ,,arg;
   }
   _hoge(
      fn ( val:int! ) {
         let! val2 = val {
            return;
         };
         print( val2 );
      }
   );
   foo( nil );
   foo( 1 );
}
{
   Macro._section();
   macro _hoge( val:__exp ) : __exp {
      ,,val
   }

   fn sub() : int, int!, int {
      return 1, 2, 3;
   }
   let bar = _hoge(
      fn ( arg1:int! ) {
         let! foo, foo2 = sub() {
            return;
         };
         print( arg1, foo + 100, foo2 + 100 );
      });
   bar( nil );
}
{
   Macro._section();
   macro _hoge() : __exps {
      sub()
   }
   fn sub() : int, int! {
      return 1, 2;
   }
   let val1, val2 = _hoge();
   print( val1, val2 );

}
{
   Macro._section();
   macro _hoge( val:__exps ) {
      print( ,,val** );
   }
   fn sub() : int, int {
      return 1, 2;
   }
   _hoge( sub() );
}
{
   Macro._section();
   fn functest() {
      print( __func__ );
   }
   fn functest2() {
      print( __func__ );
   }
   __scope root ( functest, functest2 ) {
      functest();
      functest2();
      print( "hoge" );
   }
}
{
   Macro._section();
   print(-1); // (-1) の parse が無限ループになっていた不具合があったので、その再発防止用
}
{
   Macro._section();
   class Testaaa<T> {
      pub let val:T;
   }
   let test = new Testaaa( 1 );
   print( test.val + 1); // generic のメンバを、直接アクセスした場合のテスト
}
{
   Macro._section();
   fn func() : int!, int! {
      return nil, nil;
   }

   let! _, val2 = func() { // _ は unwrap ブロックでセットしなくて良い
      val2 = 10;
   };
   print( val2 );
}
{
   Macro._section();
   macro _hoge1( var:int, var2:sym ) {
      {
         let line;
         if ,,var == 0 {
            line = `{ ,,var2( "hoge" ) };
         } else {
            line = `{ print( "hoge2" ) };
         }
      }
      ,,line;
   }

   macro _hoge2( var:int, var2:sym ) {
      _hoge1( ,,var, ,,var2 );
   }
   _hoge2( 0, print );
   _hoge2( 1, print );
}
{
   Macro._section();
   macro _hoge1( var:bool, var2:sym ) {
      {
         let line;
         if ,,var {
            line = `{ ,,var2( "hoge" ) };
         } else {
            line = `{ print( "hoge2" ) };
         }
      }
      ,,line;
   }

   macro _hoge2( var:bool, var2:sym ) {
      _hoge1( ,,var, ,,var2 );
   }
   _hoge2( false, print );
}
{
   Macro._section();
   macro _hoge1( var:sym, var2:sym ) {
      {
         let line;
         if ,,,,var == "print" {
            line = `{ ,,var2( "hoge" ) };
         } else {
            line = `{ print( "hoge2" ) };
         }
      }
      ,,line;
   }

   macro _hoge2( var:sym, var2:sym ) {
      _hoge1( ,,var, ,,var2 );
   }
   _hoge2( print, print );
}
{
   Macro._section();
   class Sub<X:(Mapping)> extend (Mapping) {
      let val:X {pub};
   }
   class Test222<T:(Mapping)> extend (Mapping) {
      let val:Sub<T> {pub}; // [X] = T
   }

   {
      let test = new Test222( new Sub( "abc" ) );
      print( test.$val.$val );
      let map = test._toMap();
      let test2 = unwrap Test222<str>._fromMap( map );
      print( test2.$val.$val );
      let _, mess = Test222<Sub<str>>._fromMap( map );
      print( mess );
   }
}
{
   Macro._section();
   class Super {
      let val:int {pub};
   }
   class Sub extend Super {
   }

   class Test<T:Super> {
      let mut val:Super;
      pub fn func( val:T ) mut : &T {
         print( self.val.$val );
         self.val = val;
         print( self.val.$val );
         return self.val;
      }
   }
   let mut test = new Test<Sub>( new Sub( 1 ) );
   test.func( new Sub( 2 ));
}
{
   Macro._section();
   fn foo( test_:str ) {
      __scope root ( test_ ) {
         fn func( val:str ) {
            print( test_ .. "abc" .. val );
         }
         func( test_ );
      }
   }
   foo( "123" );
}
{
   Macro._section();
   class Hoge {
   }
   pub fn Hoge.func();
   pub fn Hoge.func()
   {
      print( __func__ );
   }
   let hogess = new Hoge();
   hogess.func();
}
{
   Macro._section();
   fn func():...<int!> {
      return 1, 2;
   }
   if! func() {
      print( _exp + 10 );
   }
}
_ = 1;
{
   _ = 2;
}
{
   Macro._section();
   {
      fn func():...<int!> {
         return 1,2;
      }
      if func() {
         print( "abc");
      }
   }
   {
      fn func():...<int!> {
         return ;
      }
      if func() {
         print( "xyz");
      }
   }
}
{
   Macro._section();
   fn func(): int! {
      return 1;
   }
   fn sub() {
      let val:int;
      unwrap! val = func() {
         return;
      };
      print( val );
   }
   sub();
}
{
   Macro._section();
   let test = new SUB2.Test5( "aaa" );
   print( test.$val );
}
{
   Macro._section();
   Sub8.sub();
}
{
   Macro._section();
   macro _hoge( val:__exp) {
      ,,val;
   }
   _hoge( print("hoge") );
}
{
   Macro._section();
   abstract class Test<T> {
      let mut val:T! {pub};
      pro fn __init( val:T! ) {
         self.val = val;
      }
   }

   class Sub extend Test<int> {
   }

   let sub = new Sub( 1 );
   print( sub.$val );
}
{
   Macro._section();
   macro _hoge( list:List<int> ) {
      {
         foreach val in list {
            let valName;
            valName = "%s" (val);
         }
      }
   }
}
{
   Macro._section();
   macro _hoge( val:int ) {
      {
         let mut hoge;
         {
            let valName;
            valName = "%s" (val);
            hoge = valName;
         }
      }
      print( ,,hoge .. "a" );
   }
   _hoge( 10 );
}
{
   Macro._section();
   macro _hoge( func:__exps ) {
      print( ,,func** );
   }
   fn func() : int, int {
      return 1, 2;
   }
   _hoge( func() );
}
{
   Macro._section();
   class Hoge{
      let mut val:int{non,pri};
      pub fn __init() {
         self.val = 1;
      }
      pub fn func() mut {
         self.set_val(1);
      }
   }
   pub fn Hoge.func2() mut {
      self.set_val(2);
   }
   pub fn Hoge.get() : int {
      return self.val;
   }

   let mut hoge2 = new Hoge();
   hoge2.func();
   print( hoge2.get() );
   hoge2.func2();
   print( hoge2.get() );
}
{
   Macro._section();
   class Hoge {
      let val:int {pub};
   }
   let mut paramList:List<int> = [];
   for index = 1, 10 {
      paramList.insert( index );
   }
   let seed = (os.clock() * 1000000)@@int;
   math.randomseed( seed );
   let mut list:List<&Hoge> = [];
   for _ = 1, #paramList {
      let index = math.random(1,#paramList)@@int;
      let val = paramList[ index ];
      paramList.remove( index );
      list.insert( new Hoge( val ) );
   }
   list.sort( fn ( val1:&Hoge, val2:&Hoge): bool {
      return val1.$val < val2.$val;
   });
   foreach val in list {
      print( val.$val );
   }
}
{
   Macro._section();
   print(__lns.runMode.Queue, __lns.runMode.Skip, __lns.runMode.Sync );
}
{
   Macro._section();
   macro _test5( hoge:__block ) {
      fn func()
      ,,hoge;
   }
   _test5( {
      print( "hoge" );
   });

   func();
   func();
}
{
   Macro._section();
   class Hoge extend (Mapping) {
      let val1:int;
      let val2:str;
   }
   class Hoge2 extend Hoge {
      let val3:real;
   }

   fn func( obj:&Mapping ) : &Map<str,&stem> {
      let map = obj._toMap();
      return map;
   }

   let hoge2:&Hoge = new Hoge2( 1, "abc", 10.5 );
   forsort val, key in func( hoge2 ) {
      print( key, val );
   }
}
Macro._section();
__test {
   class Hoge {
      let val:int;
      pub fn __init() {
         // __test ブロック内のクラスのコンストラクタの
         // メンバ初期化検知が正常に動作していなかったのをチェックする。       
         self.val = 1; 
      }
   }
}
{
   Macro._section();
   macro _hoge( val1:__exp, val2:__exp ) {
      print( ,,val1, ,,val2 );
   }

   _hoge( 1, 2 );
   _hoge( 1 );
   _hoge();
}
{
   Macro._section();
   print( "%8.2f" (1.2345678) );
   print( "%8.2f" (12.345678) );
   print( "%8.2f" (123.45678) );
   print( "%8.2f" (1234.5678) );
}
{
   Macro._section();
   class AltBase<T> {
      let val:T! {pub};
      fn __init( val:T! ) {
         self.val = val;
      }
      pub static fn valid( val:T ): AltBase<T> {
         return new AltBase<T>( val );
      }
   }
   let val = AltBase<str>.valid( "abc" );
   print( val.$val );
}
{
   Macro._section();
   match SUB2.AAA.Val1(1,"str") {
      case .Val1( t1, t2 ) {
         print( t1 + 10, t2 .. "abc" );
      }
   }
   SUB2.AAAfunc( .Val1( "abc", 1 ) );
}
{
   Macro._section();
   SUB2.genericFunc( 999 );
}
{
   Macro._section();
   class AAA extend Sub11.Class {
      fn __free() {             // skip-lua51
         print( __func__ );     // skip-lua51
      }                         // skip-lua51
   }
   new AAA();
   collectgarbage();
}
{
   Macro._section();
   macro _hoge( flag:bool! ) {
      {
         let stat1;
         when! flag {
            stat1 = `{ print( ,,, flag ); };
         } else {
            stat1 = `{ print( ,,, flag ); };
         }
      }
      ,,stat1;
   }

   macro _hoge2( flag:bool! ) {
      {}
      _hoge( ,,flag );
   }

   _hoge2( nil );
   _hoge2( false );
   _hoge2( true );
}

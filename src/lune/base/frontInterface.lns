/*
MIT License

Copyright (c) 2018 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

import lune.base.Util;

_lune_control ignore_symbol_;

pub class ModuleMeta {
   let metaInfo:Luaval<&stem> {pub};
   let lnsPath:str {pub};
}

/**
モジュールの識別情報を管理する。
*/
pub class ModuleId {
   /** ビルドした時の lns ファイルの最終更新日時 */
   let modTime:real {pub};
   /**
meta ファイルの内容が何回変ったか。
対象の lns ファイル自体に修正がなくても、import しているモジュールが変った結果、
meta ファイルの内容が変った場合はこのカウンタをインクリメントする。
逆に lns ファイル自体に修正があっても、
meta ファイルの内容が変らない場合はこのカウンタをインクリメントしない。
   */
   let buildCount:int {pub};
   let idStr:str {pub};
   pub static let tempId:ModuleId;

   pub fn __init( modTime:real, buildCount:int ) {
      self.modTime = modTime;
      self.buildCount = buildCount;
      self.idStr = "%f:%d" (modTime, buildCount );
   }

   pub fn getNextModuleId(): ModuleId {
      return new ModuleId( self.modTime, self.buildCount + 1 );
   }
   __init {
      ModuleId.tempId = new ModuleId( 0.0, 0 );
   }
}
pub static fn ModuleId.createId( modTime:real, buildCount:int ):ModuleId {
   return new ModuleId( modTime, buildCount );
}
pub static fn ModuleId.createIdFromTxt( idStr:str ):ModuleId {
   let modTime = unwrap tonumber( ( idStr.gsub( ":.*", "" ) ) ## ) default 0.0;
   let buildCount = unwrap tonumber( ( idStr.gsub( ".*:", "" ) ) ## ) default 0.0;
   return new ModuleId( modTime, buildCount@@int );
}



/**
インポートしたモジュールを管理する。
*/
pub class ImportModuleInfo {
   let mut orderedSet:Util.OrderedSet<str>;

   pub fn __init() {
      self.orderedSet = new Util.OrderedSet<str>();
   }

   pub fn add( modulePath:str ) mut : bool {
      return self.orderedSet.add( modulePath );
   }

   pub fn remove() mut {
      self.orderedSet.removeLast();
   }

   pub fn getFull() :str {
      let mut txt = "";
      foreach modulePath in self.orderedSet.$list {
         txt = "%s -> %s" ( txt, modulePath );
      }
      return txt;
   }
}

pub interface frontInterface {
   /**
指定の LuneScript のモジュールをロードする。

このメソッドは、 トランスコンパイルと実行を同時に行なうケースで、
Lua からも利用される。

@param mod モジュール (foo.bar)
@return stem! ロードしたモジュール。 失敗した場合 nil
@return &stem ロードしたモジュールのメタ情報。
   */
   pub fn loadModule( mod:str ) mut: Luaval<&stem>!, &ModuleMeta;

   /**
指定の LuneScript のモジュールからメタ情報をロードする。

@param importModuleInfo ロード時のインポートモジュールを管理する。
@param mod モジュール (foo.bar)
@return &stem ロードしたモジュールのメタ情報。
   */
   pub fn loadMeta(
      importModuleInfo:ImportModuleInfo, mod:str ) mut: &ModuleMeta!;
   /**
LuneScript のコード文字列からロードする。

@param importModuleInfo ロード時のインポートモジュールを管理する。
@param ロード名
@param txt LuneScript のコード文字列
@return stem ロードしたモジュール。
   */
   pub fn loadFromLnsTxt(
      importModuleInfo:ImportModuleInfo, name:str, txt:str ): Luaval<&stem>;

   /**
go/github:com.ifritJP. 等の go 用のパスを、
そのモジュールのプロジェクトを起点としたモジュールパスに変換する。

詳しくは、GoMod.lns を参照。
   */
   pub fn getLuaModulePath( mod:str ) mut : str;
   pub fn searchModule( mod:str ) mut: str!;
   pub fn error( message:str );
}

class dummyFront extend (frontInterface) {
   pub fn loadModule( mod:str ) mut: Luaval<&stem>!, &ModuleMeta {
      // error( "not implements" );
      let loaded = _load( "return {}" ## );
      let emptyTable;
      when! loaded {
         emptyTable = unwrap loaded(##);
      } else {
         error( "load error" );
      }
      let meta = new ModuleMeta( emptyTable, mod.gsub( "%.", "/" ) .. ".lns" );
      return require( mod ), meta;
   }
   pub fn loadMeta( importModuleInfo:ImportModuleInfo, mod:str ) mut : &ModuleMeta! {
      error( "not implements" );
   }
   pub fn loadFromLnsTxt(
      importModuleInfo:ImportModuleInfo, name:str, txt:str ): Luaval<&stem>
   {
      error( "not implements" );
   }
   pub fn getLuaModulePath( mod:str ) mut : str {
      error( "not implements" );
   }
   pub fn searchModule( mod:str ) mut: str! {
      error( "not implements" );
   }
   pub fn error( message:str ) {
      error( "not implements" );
   }
}

global let mut __luneScript:frontInterface = new dummyFront();

pub fn setFront( newFront:frontInterface ) {
   __luneScript = newFront;
}

pub fn loadModule( mod:str ): Luaval<&stem>!, &ModuleMeta {
   return __luneScript.loadModule( mod )**;
}

pub fn loadFromLnsTxt(
   importModuleInfo:ImportModuleInfo, name:str, txt:str ): Luaval<&stem>
{
   return __luneScript.loadFromLnsTxt( importModuleInfo, name, txt );
}

pub fn loadMeta( importModuleInfo:ImportModuleInfo, mod:str ): &ModuleMeta! {
   return __luneScript.loadMeta( importModuleInfo, mod );
}

pub fn searchModule( mod:str ): str! {
   return __luneScript.searchModule( mod );
}

pub fn getLuaModulePath( mod:str ): str {
   return __luneScript.getLuaModulePath( mod );
}

/*
MIT License

Copyright (c) 2018 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

_lune_control can_not_conv_code c;

import lune.base.LuaVer;

/**
path の最終更新日時を取得する。

@param path ファイルパス
@return 1970/1/1 0:0:0 からの秒数。 取得失敗した場合は nil。
*/
pub fn getFileLastModifiedTime( path: str ): real! {
   let mut file = io.open( path## );
   if! file {
      _exp.close();
   }
   else {
      return nil;
   }

   let mut stream = io.popen( "stat -c '%%Y' %s" (path) );
   if! stream$.read$('*a') {
      return tonumber( _exp## );
   }
   // if! stream {
   //     let val = _exp.read( '*a' );
   //     if! val {
   //         return tonumber( _exp );
   //     }
   // }
   return nil;
}


fn searchpath51( mod:str, pathPattern:str ) : str! {
   apply path of string.gmatch( pathPattern, "[^;]+" ) {
      if! let index = path.find( "%?.l[un][as]$"## ) {
         let dir = path.sub( 1, index - 1 );
         let suffix = path.sub( #path - 3 ## );
         let target = dir .. mod.gsub( "%.", "/" ) .. suffix;
         if! let mut fileObj = io.open( target## ) {
            fileObj.close();
            return target;
         }
      }
   }
   return nil;
}

pub fn getLoadedMod() : &Map<str,stem>
{
   let loaded = _load( "return package.loaded"## )$(##);
   return (unwrap loaded)@@Map<str,stem>;
}

pub fn profile( validTest:bool, func: form, path: str ): stem! {
   if not validTest {
      return func(##);
   }

   module ProFi require 'ProFi' {
      pub fn start();
      pub fn stop();
      pub fn writeReport(arg:str);
   }

   ProFi.start();

   let result = func(##);

   ProFi.stop();
   ProFi.writeReport( path );


   return result;
}

pub fn getStackTrace():str {
   let mut txt = "";
   for level = 2, 10 {
      if! let debugInfo = debug.getinfo( level ) {
         txt = txt .. "-- %s %s" ( debugInfo.short_src, debugInfo.currentline );
      }
   }
   return txt;
}


let mut lua_version:str! = nil;
pub fn getLuaVersion():str {
   when! lua_version {
      return lua_version;
   }
   let loaded = _load( "return _VERSION"## )$(##);
   let version = (unwrap loaded)@@str;
   lua_version = version;
   return version;
}

fn getCurrentVer(): &LuaVer.LuaVerInfo {
   let luaVer = getLuaVersion().gsub( "^[^%d]+", "" );
   if luaVer >= "5.3" {
      return LuaVer.ver53;
   }
   elseif luaVer >= "5.2" {
      return LuaVer.ver52;
   }
   return LuaVer.ver51;
}

pub let curVer = getCurrentVer();

form SearchpathForm( mod:str, pathPattern:str ) : str!;

let mut searchpathForm:SearchpathForm = searchpath51;
if curVer.$hasSearchPath {
   searchpathForm = (unwrap _G.package$.searchpath)@@SearchpathForm;
}

pub fn searchpath( mod:str, pathPattern:str ) : str! {
   return searchpathForm( mod, pathPattern );
}

pub fn existFile( path:str ): bool {
   let! mut fileObj = io.open( path## ) {
      return false;
   };
   fileObj.close();
   return true;
}

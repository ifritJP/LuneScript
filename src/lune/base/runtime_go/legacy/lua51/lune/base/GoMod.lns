_lune_control default_async_all;

import lune.base.Types;
import lune.base.Util;
import lune.base.Depend;
import lune.base.Log;

pub class ModProjInfo {
   /** モジュールのパス */
   let path:str {pub};
   /** モジュールが属するプロジェクトパス */
   let projRoot:str {pub};
   /** projRoot からのモジュールパス */
   let mod:str {pub};
   /** 完全限定名 */
   let fullMod:str {pub};
}

pub alge GoModResult {
   NotGo,
   NotFound,
   Found(&ModProjInfo),
}

pub class ModInfo {
   /** モジュール名 */
   let name:str {pub};
   /** モジュールパス → バージョン。 go.mod の情報。 */
   let moduleMap:&Map<str,str> {pub};
   /** モジュールパス → replace 先。 go.mod の情報。 */
   let replaceMap:&Map<str,str> {pub};

   /** convPath で変換した path → 対応するローカルパス */
   let path2modProjInfo:Map<str,&ModProjInfo>;
   /** go モジュールディレクトリ → Lns 上のモジュールパス。 */
   let goModDir2Path:Map<str,str>;
   

   /** convLocalModulePath() で最後にマッチしたパス */
   let mut latestModProjInfo:&ModProjInfo!;

   fn getGoModPath( ver:str, mod:str ) : &List<str> {
      let pathList:List<str> = [];
         
      if! let gopath = Depend.getGOPATH() {
         pathList.insert( Util.pathJoin( gopath, "src/%s" ( mod ) ) );

         let mut gomod = "";
         foreach aChar in [ mod.byte(1,#mod) ] {
            when! aChar {
               if aChar >= ?A and aChar <= ?Z {
                  gomod = "%s!%c" (gomod, aChar - ?A + ?a);
               } else {
                  gomod = "%s%c" (gomod, aChar);
               }
            }
         }
         gomod = "%s@%s" (gomod,ver);
         
         pathList.insert( Util.pathJoin( gopath, "pkg/mod/%s" (gomod) ) );
      }
      return pathList;
   }
   
   pub fn __init( name:str, moduleMap:&Map<str,str>, replaceMap:&Map<str,str> ) {
      self.name = name;
      self.moduleMap = moduleMap;
      self.replaceMap = replaceMap;
      self.goModDir2Path = {};
      self.path2modProjInfo = {};
      self.latestModProjInfo = nil;

      foreach dst, mod in replaceMap {
         self.goModDir2Path[ dst ] = mod;
      }
      foreach ver, mod in moduleMap {
         foreach path in self.getGoModPath( ver, mod ) {
            self.goModDir2Path[ path ] = mod;
         }
      }
   }

   pub fn getLatestProjRoot():str! {
      return self.latestModProjInfo$.$projRoot;
   }

   fn getLocalModulePathList( path:str ) : List<str> {
      let mut pathList:List<str> = [];
      foreach ver, mod in self.moduleMap {
         if path.find( mod, 1, true ) == 1 {
            let relativeName = path.sub( #mod + 2 ## );
            
            if! let replacePath = self.replaceMap[ mod ] {
               // replace が指定されている場合、
               // パスを replace に置き換え
               pathList.insert( Util.pathJoin( replacePath, relativeName ) );
               break;
            }
            
            if! let gopath = Depend.getGOPATH() {
               pathList.insert( Util.pathJoin( gopath, "src/%s" ( path ) ) );


               let mut gomod = "";
               foreach aChar in [ mod.byte(1,#mod) ] {
                  when! aChar {
                     if aChar >= ?A and aChar <= ?Z {
                        gomod = "%s!%c" (gomod, aChar - ?A + ?a);
                     } else {
                        gomod = "%s%c" (gomod, aChar);
                     }
                  }
               }
               gomod = "%s@%s" (gomod,ver);
               
               pathList.insert(
                  Util.pathJoin( gopath, "pkg/mod/%s/%s" (gomod, relativeName) ) );
            }
            break;
         }
      }
      return pathList;
   }

   fn convPath( mod:str, suffix:str ):str {
      return mod.gsub( "^go/", "" ).gsub( "%.", "/").gsub( ":", "." ) .. suffix;
   }

   fn getProjRootPath( mod:str, mut path:str ) : str, str {
      let mut convPath =
         self.convPath( mod, ".lns" ).gsub( "github%.com/[^/]+/[^/]+/", "" );
      let mut projRoot = path.sub( 1, #path - #convPath );
      if projRoot ~= "/" {
         projRoot = projRoot.gsub( "/$", "" );
      }

      // path から projRoot までの間に lune.js があるディレクトリを探し、
      // そこを起点としたモジュールパスを返す。
      path = Util.parentPath( path );
      let mut modList = Util.splitStr( mod, "[^%.]+" );
      let mut startIndex = #modList;
      for modIndex = 1, #modList {
         if Depend.existFile( Util.pathJoin( path, "lune.js" ) ) {
            startIndex = modIndex;
            break;
         }
         if path == projRoot {
            startIndex = modIndex;
            break;
         }
         
         path = Util.parentPath( path );
      }
      // path を起点としたモジュールパスを生成
      let mut convMod = "";
      for index = #modList - startIndex + 1, #modList {
         if convMod ~= "" {
            convMod = "%s." (convMod);
         } 
         convMod = convMod .. modList[index];
      }
      return path, convMod;
   }
   
   /**
go/github:com.ifritJP. 等の go 用のモジュールパスを、
ローカルのファイルパスに変換する
   */ 
   pub fn convLocalModulePath( mut mod:str, suffix:str, baseDir:str! ) mut : GoModResult {
      if not mod.find( "^go/" ## ) {
         when! baseDir {
            let mut goModDir:str! = nil;
            goModDir = self.goModDir2Path[ baseDir ];
            when! goModDir {
               mod = "go/%s.%s" (goModDir.gsub("%.", ":").gsub( "/", "." ), mod);
            }
            if not goModDir {
               Log._log2( .Log, "not found baseDir -- %s" (baseDir) );
            }
         } else {
            return .NotGo;
         }
      }
      
      let mut workMod = self.convPath( mod, suffix );
      // when! goModDir {
      //    workMod = Util.pathJoin( goModDir, workMod );
      // }

      if! self.path2modProjInfo[ workMod ] {
         self.latestModProjInfo = _exp;
         return .Found( _exp );
      }
      
      let mut pathList = self.getLocalModulePathList( workMod );
      pathList.insert( Util.pathJoin( "vendor", workMod ) );

      foreach path in pathList {
         if Depend.existFile( path ) {
            let projRoot, convMod = self.getProjRootPath( mod, path );
            let projInfo = new ModProjInfo( path, projRoot, convMod, mod );
            self.path2modProjInfo[ workMod ] = projInfo;
            self.latestModProjInfo = projInfo;
            return .Found( projInfo );
         } else {
            Log._log2( .Log, "not found %s" (path) );
         }
      }
      return .NotFound;
   }

   /**
go/github:com.ifritJP. 等の go 用のパスを、
そのモジュールのプロジェクトを起点としたモジュールパスに変換する。
 
例えば go/github:com.ifritJP.LuneScript.src.lune.base.Nodes を、
lune.base.Nodes に変換する。

プロジェクトの起点は、以下の場所を探す。
- lune.js のあるディレクトリ
- github.com の場合、 github.com/XXXXX/YYYYY ディレクトリ

@return str プロジェクトからの相対モジュール
@return str! プロジェクトへのパス
@return str モジュールの完全パス
*/
   pub fn getLuaModulePath( mod:str, baseDir:str! ) mut : str, str!, str {
      let info;
      _match self.convLocalModulePath( mod, ".lns", baseDir ) {
         case .NotGo {
            return mod, nil, mod;
         }
         case .NotFound {
            return mod, nil, mod;
         }
         case .Found( workInfo ) {
            info = workInfo;
         }
      };
      return info.$mod, info.$projRoot, info.$fullMod;
   }
}

enum BlockKind {
   None,
   Require,
   Replace,
}

fn getReplace( map:Map<str,str>, tokenList:&List<str>, modIndex:int ) {
   let mut prevArrow = false;
   foreach token in tokenList {
      if token == "=>" {
         prevArrow = true;
      } elseif prevArrow {
         map[ tokenList[ modIndex ] ] = token;
         break;
      }
   }
}

pub fn getGoMap() : ModInfo {
   let mut requireMap:Map<str,str> = {};
   let mut replaceMap:Map<str,str> = {};
   let mut name = "lnsc";
   if! let mut file = io.open( "go.mod"## ) {
      let mut inBlock = BlockKind.None;
      while true {
         let! line = file.read( "*l" ) {
            break;
         };
         let trimedLine = line.gsub( "^%s", "" );
         let tokenList = Util.splitStr( trimedLine, "[^%s]+" );
         _switch inBlock {
            case .Require {
               if line.find( "^%)"## ) {
                  inBlock = .None;
               } else {
                  requireMap[ tokenList[ 1 ] ] = tokenList[ 2 ];
               }
            }
            case .Replace {
               if line.find( "^%)"## ) {
                  inBlock = .None;
               } else {
                  getReplace( replaceMap, tokenList, 1 );
               }
            }
            case .None {
               if line.find( "^module%s+" ## ) {
                  name = tokenList[ 2 ];
               } elseif line.find( "^require%s+[^%(]"## ) {
                  if #tokenList == 3 {
                     requireMap[ tokenList[ 2 ] ] = tokenList[ 3 ];
                  }
               } elseif line.find( "^require%s+%("## ) {
                  inBlock = .Require;
               } elseif line.find( "^replace%s+[^%(]"## ) {
                  getReplace( replaceMap, tokenList, 2 );
               } elseif line.find( "^replace%s+%("## ) {
                  inBlock = .Replace;
               }
            }
         }
      }
      file.close();
   }
   let mut modInfo = new ModInfo( name, requireMap, replaceMap );
   return modInfo;
}

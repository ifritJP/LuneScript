/*
MIT License

Copyright (c) 2018, 2019 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

_lune_control default_async_all;

import lune.base.Parser;
import lune.base.Ast;
import lune.base.Nodes;
import lune.base.Util;
import lune.base.Macro;

import lune.base.Types;

pub enum DeclClassMode {
   Class,
   Interface,
   Module,
   LazyModule,
}

pub class Modifier {
   /** true 縺ｮ蝣ｴ蜷医ｯ mut 蛻ｶ蠕｡縺梧怏蜉ｹ */
   let mut validMutControl: bool {non,pub};
   let processInfo:Ast.ProcessInfo;
   
   pub fn createModifier(
      typeInfo:&Ast.TypeInfo, mutMode: Ast.MutMode ) __async mut : &Ast.TypeInfo
   {
      if not self.validMutControl {
         return typeInfo;
      }
      return self.processInfo.createModifier( typeInfo, mutMode );
   }
}

pub class NSInfo {
   let mut nobody:bool {pub,pub};
   let typeInfo:Ast.TypeInfo {pub};
   let pos:&Parser.Position {pub};

   /** ループ系制御構文(for 等)の Scope */
   let mut loopScopeQueue: List<&Ast.Scope> {pub};
   
   /** __asyncLock した時の loopScopeQueue の長さ */
   let mut lockedAsyncStack:List<int>;

   
   pub fn isLockedAsync() : bool {
      return #self.lockedAsyncStack > 0;
   }

   pub fn __init( typeInfo:Ast.TypeInfo, pos:&Parser.Position ) {
      self.nobody = false;
      self.lockedAsyncStack = [];
      self.loopScopeQueue = [];
      
      self.typeInfo = typeInfo;
      self.pos = pos;
   }

   pub fn incLock() mut {
      self.lockedAsyncStack.insert( #self.loopScopeQueue );
   }
   pub fn decLock() mut {
      self.lockedAsyncStack.remove(##);
   }

   /**
break が可能かどうか調べる。

__asyncLock されていない場合、
loopScopeQueue の数でループ内部かどうかを判断し、ループ内の場合は break 可能。

__asyncLock されている場合、
lockedAsyncStack に格納されている情報よりも、今のループ数の方が多い場合は break 可能。
   */ 
   pub fn canBreak() : bool {
      let len = #self.lockedAsyncStack;
      let loopQueueLen = #self.loopScopeQueue;
      if len == 0 {
         return loopQueueLen > 0;
      }
      return self.lockedAsyncStack[ len ] < loopQueueLen;
   }

   /**
現在の状態で __noasync にアクセス可能かどうか。
*/
   pub fn canAccessNoasync() : bool {
      if self.typeInfo.$asyncMode == .Noasync or #self.lockedAsyncStack > 0 {
         return true;
      }
      return false;
   }
}

pub interface TransUnitIF {
   pub fn get_scope() mut : Ast.Scope;
   pub fn error( mess: str ) mut : __;
   pub fn pushModuleLow(
      processInfo:Ast.ProcessInfo,
      externalFlag: bool, name:str, mutable: bool ) mut : &Ast.TypeInfo;
   pub fn pushModule(
      processInfo:Ast.ProcessInfo,
      externalFlag: bool, name:str, mutable: bool ) mut : NSInfo;
   pub fn popModule() mut;
   pub fn pushClassScope(
      errPos:&Parser.Position, classTypeInfo:&Ast.TypeInfo, scope:Ast.Scope ) mut;
   pub fn popClass() mut;
}


pub class TransUnitBase extend (TransUnitIF) {

   pro let processInfo:Ast.ProcessInfo;
   pro let mut scope:Ast.Scope;
   pro let namespace2Scope: Map<&Ast.TypeInfo,Ast.Scope>;
   /** パーサー */
   pro let mut parser: Parser.DefaultPushbackParser;

   /**
名前空間タイプ Map。
解析終了時に、この Map に nobody 設定が残っているとエラー。

TypeInfo → NSInfo.
   */
   pro let mut nsInfoMap:Map<&Ast.TypeInfo,NSInfo>;

   // /** 解析済みトークンリスト */
   // pri let mut usedTokenList: List<&Parser.Token>;
   /** 解析エラーメッセージ */
   pro let mut errMessList: str[] { pub };
   pro let mut warnMessList: str[] { pub };

   pro let mut typeNameCtrl:&Ast.TypeNameCtrl;

   /** クラスの TypeId -> NamespaceInfo */
   pro let mut typeId2ClassMap: Map<&Ast.IdInfo,&Nodes.NamespaceInfo>;
   
   pro let mut macroCtrl:Macro.MacroCtrl;

   
   pub fn get_scope() mut : Ast.Scope {
      return self.scope;
   }
   
   pub fn __init(
      ctrl_info: &Types.TransCtrlInfo, processInfo:Ast.ProcessInfo, scope:Ast.Scope,
      parser:Parser.DefaultPushbackParser, macroEval: &Nodes.MacroEval )
   {
      self.macroCtrl = new Macro.MacroCtrl( macroEval );
      self.typeId2ClassMap = {};
      self.typeNameCtrl = Ast.defaultTypeNameCtrl;
      self.errMessList = [];
      self.warnMessList = [];
      self.namespace2Scope = {};
      self.processInfo = processInfo;
      self.scope = scope;
      self.nsInfoMap = {};
      let mut subRootTypeInfo = self.processInfo.$dummyParentType;
      self.nsInfoMap[ subRootTypeInfo ] = new NSInfo(
         subRootTypeInfo, new Types.Position( 0,0, "@builtin@" ) );
      self.parser = parser;
   }

   pro fn addErrMess( pos: &Parser.Position, mut mess: str ) __async mut {
      if mess.find( "type mismatch.*<- &" ##) {
         mess = mess .. ". if your code is the old style, use the opiton '--legacy-mutable-control'.";
      }
      self.errMessList.insert(
         "%s:%d:%d: error: %s" ( pos.streamName,
                                 pos.lineNo, pos.column, mess ) );
   }

   
   pub fn errorAt( pos:&Parser.Position, mess: str ) __async mut : __ {
      self.addErrMess( pos, mess);

      foreach errmess in self.errMessList {
         Util.errorLog( errmess );
      }
      foreach warnmess in self.warnMessList {
         Util.errorLog( warnmess );
      }
      if self.macroCtrl.$analyzeInfo.$mode ~= .None {
         print( "------ near code -----", self.macroCtrl.$analyzeInfo.$mode.$_txt );
         print( self.parser.getNearCode() );
         print( "------" );
      }
      
      Util.err( "has error" );
   }

   pub fn error( mess: str ) __async mut : __ {
      self.errorAt( self.parser.getLastPos(), mess );
   }

   pub fn pushScope( classFlag: bool, baseInfo: &Ast.TypeInfo!,
                     interfaceList: &List<&Ast.TypeInfo>! ) mut: Ast.Scope
   {
      self.scope = Ast.TypeInfo.createScope(
         self.processInfo, self.scope, classFlag, baseInfo, interfaceList );
      return self.scope;
   }

   pub fn popScope() mut {
      self.scope = self.scope.get_parent();
   }

   /**
解析した名前空間の情報を登録する。
この情報は、&TypeInfo から imut を外すために利用する。
   */ 
   pro fn newNSInfo(
      typeInfo:Ast.TypeInfo, pos:&Parser.Position) __async mut : NSInfo
   {
      let mut nsInfo = new NSInfo( typeInfo, pos );
      self.nsInfoMap[ typeInfo ] = nsInfo;
      return nsInfo;
   }
   
   /**
現在のスコープが属する名前空間の型を取得する。

※
現在のスコープが属する名前空間が、
この TransUnit で生成した名前空間でない場合はエラーするので注意すること。

例えば、現在のスコープが rootScope だった場合はエラーする。
*/ 
   pro fn getCurrentNamespaceTypeInfoMut() __async mut : Ast.TypeInfo {
      let typeInfo = self.scope.getNamespaceTypeInfo();
      let! mut nsInfo = self.nsInfoMap[ typeInfo ] {
         self.error( "not found nsInfo -- %s (%d)"
                     (typeInfo.getTxt(##), typeInfo.$typeId.id) );
      };
      return nsInfo.$typeInfo;
   }
   
   pro fn getCurrentNamespaceTypeInfo() : &Ast.TypeInfo {
      return self.scope.getNamespaceTypeInfo();
   }

   pub fn pushModule(
      processInfo:Ast.ProcessInfo,
      externalFlag: bool, name:str, mutable: bool ) mut : NSInfo
   {
      let mut typeInfo:&Ast.TypeInfo = Ast.headTypeInfo;

      let modName;
      if name.find( "^@" ## ) {
         modName = name;
      }
      else {
         modName = "@%s" (name);
      }

      let mut nsInfo;
      if! self.scope.getTypeInfoChild( modName ) {
         typeInfo = _exp;
         if! let mut scope = self.namespace2Scope[ typeInfo ] {
            self.scope = scope;
         } else {
            self.error( "not found scope -- %s" (name) );
         }
         nsInfo = unwrap self.nsInfoMap[ typeInfo ];
      }
      else {
         let mut parentInfo = self.getCurrentNamespaceTypeInfoMut();
         let mut parentScope = self.scope;
         let mut scope = self.pushScope( true ## );
         let mut newType = processInfo.createModule(
            scope, parentInfo, externalFlag, modName, mutable );
         typeInfo = newType;
         self.namespace2Scope[ typeInfo ] = scope;
         nsInfo = self.newNSInfo( newType, self.parser.getLastPos() );

         let _, existSym = parentScope.addClass( processInfo, modName, nil, typeInfo );
         when! existSym {
            self.addErrMess(
               self.parser.getLastPos(),
               "module symbols exist -- %s.%s -- %s.%s"
               ( existSym.$namespaceTypeInfo.getFullName(
                  self.typeNameCtrl, parentScope, false ),
                 existSym.$name,
                 parentInfo.getFullName( self.typeNameCtrl, parentScope, false ),
                 modName ) );
         }
      }
      if not self.typeId2ClassMap[ typeInfo.$typeId ] {
         let namespace = new Nodes.NamespaceInfo( modName, self.scope, typeInfo );
         self.typeId2ClassMap[ typeInfo.$typeId ] = namespace;
      }
      return nsInfo;
   }
   
   pub fn pushModuleLow(
      processInfo:Ast.ProcessInfo,
      externalFlag: bool, name:str, mutable: bool ) mut : &Ast.TypeInfo
   {
      return self.pushModule( processInfo, externalFlag, name, mutable ).$typeInfo;
   }

   pub fn popModule() mut {
      self.popScope();
   }

   /**
既存クラスの Scope を push する。
   */
   pub fn pushClassScope(
      errPos:&Parser.Position, classTypeInfo:&Ast.TypeInfo, scope:Ast.Scope ) mut
   {
      if self.scope ~= classTypeInfo.$scope$.$parent {
         let classParentName;
         let classParentTypeId;
         if! let parentType = classTypeInfo.$scope$.$parent$.$ownerTypeInfo {
            classParentName = parentType.getFullName(
               self.typeNameCtrl, unwrap parentType.$scope, false );
            classParentTypeId = parentType.$typeId;
         }
         else {
            classParentName = "nil";
            classParentTypeId = Ast.dummyIdInfo;
         }
         self.addErrMess(
            errPos, "This class does not exist in this scope. -- %s -- %s(%d), %s(%d)"
            ( classTypeInfo.getTxt( ## ),
              self.scope.$ownerTypeInfo$.getFullName$(
                 self.typeNameCtrl, self.scope, false ),
              self.scope.$ownerTypeInfo$.$typeId$.id or -1,
              classParentName, classParentTypeId.id ) );
      }
      self.scope = scope;
   }


   /**
指定クラスの Scope を push し、TypeInfo を取得する。

クラスが生成済みの場合は baseInfo, interfaceList, genTypeList に nil を指定する。
   */
   pub fn pushClass(
      processInfo:Ast.ProcessInfo,
      errPos:&Parser.Position, mode:DeclClassMode, abstractFlag: bool,
      baseInfo: &Ast.TypeInfo!, interfaceList: &List<&Ast.TypeInfo>!,
      genTypeList: &List<&Ast.AlternateTypeInfo>!, externalFlag: bool, name:str,
      allowMultiple:bool, accessMode:Ast.AccessMode,
      defNamespace: Nodes.NamespaceInfo! ) mut : NSInfo
   {
      let typeInfo:&Ast.TypeInfo;
      let mut nsInfo;
      if! self.scope.getTypeInfo( name, self.scope, true, .Normal ) {
         // 宣言済み
         typeInfo = _exp;
         nsInfo = unwrap self.nsInfoMap[ typeInfo ];

         if typeInfo.$scope$.$parent ~= self.scope {
            self.addErrMess(
               errPos,     
               "multiple class(%s)" (typeInfo.getTxt( self.typeNameCtrl ##)));
            // クラスの複数定義は、その後の解析すると、
            self.error( "stop by error" );
         }

         if typeInfo.$abstractFlag ~= abstractFlag {
            self.addErrMess( errPos,
                             "mismatch class(%s) abstract for prototpye"
                             (typeInfo.getTxt( self.typeNameCtrl ##)));
         }
         if typeInfo.$accessMode ~= accessMode {
            self.addErrMess( errPos,
                             "mismatch class(%s) accessmode(%s) for prototpye accessmode(%s)"
                             ( typeInfo.getTxt( self.typeNameCtrl ##),
                               accessMode.$_txt, typeInfo.$accessMode.$_txt));
         }
         when! baseInfo {
            if typeInfo.$baseTypeInfo ~= baseInfo {
               self.addErrMess(
                  errPos,
                  "mismatch class(%s) base class(%s) for prototpye base class(%s)"
                  ( typeInfo.getTxt( self.typeNameCtrl##),
                    baseInfo.getTxt(##), typeInfo.$baseTypeInfo.getTxt(##)));
            }
         }
         else {
            if typeInfo.hasBase() {
               self.addErrMess(
                  errPos,
                  "mismatch class(%s) base class(None) for prototpye base class(%s)"
                  ( typeInfo.getTxt( self.typeNameCtrl##),
                    typeInfo.$baseTypeInfo.getTxt(##)));
            }
         }

         fn compareList( protoList:&List<&Ast.TypeInfo>,
                         typeList:&List<&Ast.TypeInfo>, message:str )
         {
            if #protoList == #typeList {
               foreach protoType, index in protoList {
                  if protoType ~= typeList[ index ] {
                     self.addErrMess(
                        errPos,
                        "mismatch class(%s) %s(%s) for prototpye %s(%s)"
                        ( typeInfo.getTxt(self.typeNameCtrl##), message,
                          typeList[ index ].getTxt(self.typeNameCtrl##),
                          message, protoType.getTxt(##)));
                  }
               }
            }
            else {
               self.addErrMess(
                  errPos,
                  "mismatch class(%s) %s(%d) for prototpye %s(%d)"
                  ( typeInfo.getTxt(self.typeNameCtrl##), message, #typeList,
                    message, #protoList) );
            }
         }
         
         compareList( typeInfo.$interfaceList,
                      unwrap interfaceList default [], "interface" );
         
         compareList( typeInfo.$itemTypeInfoList,
                      unwrap genTypeList default [], "generics" );

         

         if! let mut scope = self.namespace2Scope[ typeInfo ] {
            self.scope = scope;
         } else {
            self.errorAt( errPos, "not find scope -- %s" (name) );
         }
         switch ( typeInfo.$kind ) {
            case .Class {
               if mode == .Interface {
                  self.addErrMess(
                     errPos,
                     "define interface already -- %s" (name) );
                  Util.printStackTrace();
               }
            }
            case .IF {
               if mode ~= .Interface {
                  self.addErrMess(
                     errPos,
                     "define class already -- %s" (name) );
                  Util.printStackTrace();
               }
            }
         }
      }
      else {
         let mut parentInfo = self.getCurrentNamespaceTypeInfoMut();

         let mut parentScope = self.scope;
         let mut scope = self.pushScope( true, baseInfo, interfaceList );
         let workGenTypeList;
         when! genTypeList {
            workGenTypeList = genTypeList;
         }
         else {
            workGenTypeList = [];
         }

         let mut newType = processInfo.createClassAsync(
            mode ~= .Interface, abstractFlag, scope, baseInfo, interfaceList,
            workGenTypeList, parentInfo, externalFlag, accessMode, name );
         typeInfo = newType;
         self.namespace2Scope[ typeInfo ] = scope;
         nsInfo = self.newNSInfo( newType, errPos );
         
         parentScope.addClassLazy(
            processInfo, name, errPos, typeInfo, mode == .LazyModule );
      }
      when! genTypeList {
         foreach genType in genTypeList {
            self.scope.addAlternate(
               processInfo, accessMode, genType.$txt, errPos, genType );
         }
      }

      let! mut namespace = defNamespace {
         namespace = new Nodes.NamespaceInfo( name, self.scope, typeInfo );
      };
      self.typeId2ClassMap[ typeInfo.get_typeId() ] = namespace;
      
      return nsInfo;
   }
   
   /**
指定クラスの Scope を push し、TypeInfo を取得する。

クラスが生成済みの場合は baseInfo, interfaceList, genTypeList に nil を指定する。
   */
   pub fn pushClassLow(
      processInfo:Ast.ProcessInfo,
      errPos:&Parser.Position, mode:DeclClassMode, abstractFlag: bool,
      baseInfo: &Ast.TypeInfo!, interfaceList: &List<&Ast.TypeInfo>!,
      genTypeList: &List<&Ast.AlternateTypeInfo>!, externalFlag: bool, name:str,
      allowMultiple:bool, accessMode:Ast.AccessMode,
      defNamespace: Nodes.NamespaceInfo! ) mut : &Ast.TypeInfo
   {
      return self.pushClass(
         processInfo, errPos, mode, abstractFlag,
         baseInfo, interfaceList, genTypeList, externalFlag, name,
         allowMultiple, accessMode, defNamespace ).$typeInfo;
   }

   pub fn popClass() mut {
      self.popScope();
   }
}

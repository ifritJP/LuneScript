/*
MIT License

Copyright (c) 2018 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

      
_lune_control default_async_all;

import lune.base.Types;
import lune.base.frontInterface;
import lune.base.Parser;
import lune.base.convLua;
import lune.base.convGo;
import lune.base.AstInfo;
import lune.base.TransUnit;
import lune.base.Util;
import lune.base.Option;
import lune.base.dumpNode;
import lune.base.glueFilter;
import lune.base.Depend;
import lune.base.OutputDepend;
import lune.base.Ver;
import lune.base.LuaVer;
import lune.base.Log;
import lune.base.Formatter;
import lune.base.Testing;
import lune.base.GoMod;
import lune.base.Meta;
import lune.base.LuneControl;
import lune.base.Nodes;
import lune.base.Ast;
import lune.base.Runner;
import lune.base.Builtin;
import lune.base.NodeIndexer;

_lune_control ignore_symbol_;


Depend.setup( fn(ver:int) {
   LuaVer.setCurVer( ver );
});


let forceUpdateMeta = true;

class LoadInfo {
   /** モジュールの lua コードのロード結果 */
   pub let mut mod:Luaval<&stem>;
   /** モジュールのメタ情報 */
   pub let mut meta:frontInterface.ModuleMeta;
}

pub form AstCallback( ast: &AstInfo.ASTInfo );

alge UptodateInfo {
   /** 最新で更新不要。 .meta から情報を読み込んでいる */
   Uptodate(frontInterface.ModuleMeta),
   /** 更新済み。 .lns から .meta を構築している */
   Update(&frontInterface.ModuleInfo, &AstInfo.ASTInfo),
}

fn createModuleInfo(
   ast:&AstInfo.ASTInfo, mod:str,
   moduleId:&frontInterface.ModuleId ) __async : frontInterface.ModuleInfo
{
   // let importedAliasMap:Map<&Ast.TypeInfo,&Ast.AliasTypeInfo> = {};
   // let rootNode = unwrap ast.$node@@@Nodes.RootNode;
   // foreach node in rootNode.$nodeManager.getAliasNodeList() {
   //    importedAliasMap[ node.$typeInfo ] = node.$expType@@@Ast.AliasTypeInfo;
   // }
   let mut exportInfo = ast.$exportInfo;
   return new frontInterface.ModuleInfo( exportInfo );
}


class ModuleMgr {
   /** mod → 更新情報 */
   let mut mod2info:Util.OrderdMap<str,UptodateInfo>;
   /** mod → ロード済みメタ情報 */
   let mut loadedMetaMap:Map<str,frontInterface.ModuleMeta>;
   
   pub fn __init() {
      self.mod2info = new Util.OrderdMap<str,UptodateInfo>();
      self.loadedMetaMap = {};
   }

   pub fn get( mod:str ) mut : UptodateInfo! {
      return self.mod2info.$map[ mod ];
   }
   
   pub fn getAst( mod:str ) mut : &AstInfo.ASTInfo! {
      let! mut info = self.get( mod ) {
         return nil;
      };
      _match info {
         case .Update( _, ast ) {
            return ast;
         }
         case .Uptodate( _ ) {
            return nil;
         }
      }
   }

   pub fn getModList() : &List<str> {
      return self.mod2info.$keyList;
   }
   
   
   pub fn add( ast:&AstInfo.ASTInfo,
               moduleInfo:&frontInterface.ModuleInfo ) mut
   {
      let mod = moduleInfo.$fullName;
      // if self.loadedMetaMap[ mod ] {
      //    if mod == "lune.base.Testing" {
      //       // lune.base.Testing は テストケース解析時に自動で import される。
      //       // ここでエラーにするとテスト出来ないので、エラーにしない。
      //    } else {
      //       // meta を単独で読み込んだ後に AST 解析すると、
      //       // Ast.ProcessInfo に不整合が起るので、ここでエラーにする。
      //       Util.err( "the meta is already loaded -- %s" (mod) );
      //    }
      // }
      self.mod2info.add( mod, UptodateInfo.Update( moduleInfo, ast ) );
      // self.loadedMetaMap[ mod ] = new frontInterface.ModuleMeta(
      //    ast.$streamName, .Module( moduleInfo ));
   }

   pub fn addMeta( mod:str, meta:frontInterface.ModuleMeta ) mut {
      if not self.get( mod ) {
         self.mod2info.add( mod, UptodateInfo.Uptodate( meta ) );
      }
      self.loadedMetaMap[ mod ] = meta;
   }

   pub fn getMeta( mod:str ) mut : frontInterface.ModuleMeta! {
      return self.loadedMetaMap[ mod ];
   }
}


form ConverterFunc() __async;

class AstCreater extend Runner.Runner {
   _lune_control default_async_this_class;
   
   let option:&Option.Option;
   let builtinFunc:&Builtin.BuiltinFuncType;
   let mod:str;
   let moduleId:&frontInterface.ModuleId;
   let mut ast:&AstInfo.ASTInfo!;
   let mut moduleInfo:frontInterface.ModuleInfo!;

   let converter:ConverterFunc;
   let mut exportInfo:&frontInterface.ExportInfo!;
   let exportInfoReadyFlag:__lns.Sync.Flag!;
   
   pri fn createAst(
      importModuleInfo:&frontInterface.ImportModuleInfo,
      mut parserSrc:&Types.ParserSrc, baseDir:str!, stdinFile:&Types.StdinFile!,
      analyzeModule:str!, analyzeMode:TransUnit.AnalyzeMode,
      pos:&Parser.Position! ) mut : &AstInfo.ASTInfo
   {
      let mut transUnit = new TransUnit.TransUnit(
         self.moduleId, importModuleInfo, new convLua.MacroEvalImp( self.builtinFunc ),
         true, analyzeModule, analyzeMode, pos, self.option.targetLuaVer,
         self.option.transCtrlInfo, self.builtinFunc );

      return transUnit.createAST(
         parserSrc, true, baseDir, stdinFile, false, self.mod,
         fn ( exportInfo:&frontInterface.ExportInfo ) __trans {
            self.exportInfo = exportInfo;
            // exportInfo をセットしてから flag をセットする。
            if! self.exportInfoReadyFlag {
               _exp.set();
            }
         }
      );
   }
   
   local fn __init(
      importModuleInfo:&frontInterface.ImportModuleInfo, parserSrc:&Types.ParserSrc,
      mod:str, baseDir:str!, moduleId:&frontInterface.ModuleId,
      analyzeModule:str!, analyzeMode:TransUnit.AnalyzeMode, pos:&Parser.Position!,
      builtinFunc:&Builtin.BuiltinFuncType, option:&Option.Option )
   {
      super();
      
      self.option = option;
      self.builtinFunc = builtinFunc;
      self.mod = mod;
      self.moduleId = moduleId;
      self.moduleInfo = nil;
      self.exportInfo = nil;
      self.ast = nil;
      self.exportInfoReadyFlag = __lns.Sync.createFlag();
      
      self.converter = fn () {
         let ast = self.createAst(
            importModuleInfo, parserSrc, baseDir, option.$stdinFile,
            analyzeModule, analyzeMode, pos );
         self.ast = ast;
         self.moduleInfo = createModuleInfo( ast, self.mod, self.moduleId );
         Log._log2( .Log, "generated AST -- %s" (mod) );
      };

      let lnsPath;
      match parserSrc {
         case .LnsCode( _, path, _ ) {
            lnsPath = path;
         }
         case .LnsPath( path, _, _ ) {
            lnsPath = path;
         }
         case .Parser( path, _, _, _ ) {
            lnsPath = path;
         }
      }

      self.start( __lns.runMode.Sync, "createAst - %s" (lnsPath) );
      //self.converter();
   }

   pro override fn runMain() __async mut {
      self.converter();
   }
   

   local fn getAst() :
      &AstInfo.ASTInfo, &frontInterface.ModuleInfo, frontInterface.ModuleMeta
   {
      __join( self );
      let! exportInfo = self.exportInfo {
         Util.err( "exportInfo is nil -- %s" (self.mod) );
      };
      let mut moduleMeta = new frontInterface.ModuleMeta(
         exportInfo.$streamName, .Export( exportInfo ));
      return unwrap self.ast, unwrap self.moduleInfo, moduleMeta;
   }

   local fn getExportInfo() : &frontInterface.ExportInfo! {
      if! self.exportInfoReadyFlag {
         _exp.wait();
      }
      if not self.exportInfo {
         Log._log2( Log.Level.Err, "exportInfo is nil -- %s" (self.mod) );
      }

      return self.exportInfo;
   }
   
}

class Front extend (frontInterface.frontInterface) {
   let mut option:&Option.Option;
   /** import 済みのモジュールと、その LoadInfoの紐付け。 */ 
   let mut loadedMap:Map<str,LoadInfo>;
   /** import 済みの Test ビルド用モジュールと、その LoadInfoの紐付け。 */ 
   let mut loadedMapTest:Map<str,LoadInfo>;
   /** import 済みのモジュール → Lua コードのマップ */
   let mut convertedMap:Map<str,str>;
   let mut gomodMap:GoMod.ModInfo;

   let mut bindModuleSet:Set<str>;
   let moduleMgr:ModuleMgr;
   let targetSet:Set<str>;

   let mod2astCreate:Map<str,AstCreater>;
   let mod2loader:Map<str,frontInterface.ModuleLoader>;


   /**
front で import しているものは、
loadedMap で管理する前にシステムにロードされているため、
後で import すると多重ロードになってしまう。
多重ロードを防ぐため、 loadedMap で管理する前にロードされているモジュールを保持する。
*/
   let preloadedModMap:&Map<str,Luaval<&stem>>;

   let mut loadCount:int;

   let mut builtinFunc:&Builtin.BuiltinFuncType;

   
   pub fn __init( option:&Option.Option, bindModuleList:&List<str>! ) __noasync {
      self.mod2loader = {};
      self.mod2astCreate = {};
      self.loadCount = 0;
      self.targetSet = (@);
      self.bindModuleSet = (@);
      when! bindModuleList {
         foreach mod in bindModuleList {
            self.bindModuleSet.add( mod );
         }
      }
      self.moduleMgr = new ModuleMgr();
      self.gomodMap = GoMod.getGoMap();
      self.option = option;
      self.loadedMap = {};
      self.loadedMapTest = {};
      self.convertedMap = {};

      {
         let mut builtin = new Builtin.Builtin(
            self.option.targetLuaVer, option.transCtrlInfo );
         self.builtinFunc = builtin.registBuiltInScope();
      }
      

      frontInterface.setFront( self );

      let mut loadedMap:Map<str,Luaval<&stem>> = {};
      __luago {
         foreach modval, mod in Depend.getLoadedMod() {
            // pending:
            if mod == "lune.base.Testing" {
               loadedMap[ mod ] = modval;
            }
            if option.testing and modval.__enableTest or
               not option.testing and modval.__enableTest
            {
               loadedMap[ mod ] = modval;
            }
         }
      }
      self.preloadedModMap = loadedMap;
   }

   fn getLoadInfo( mod:str ) : LoadInfo! {
      if self.option.testing {
         return self.loadedMapTest[ mod ];
      }
      return self.loadedMap[ mod ];
   }

   fn setLoadInfo( mod:str, info:LoadInfo ) mut {
      if self.option.testing {
         self.loadedMapTest[ mod ] = info;
      }
      self.loadedMap[ mod ] = info;
   }
   

   pub fn loadModule( mod:str ) __async mut: Luaval<&stem>!, frontInterface.ModuleMeta;
   pub fn loadMeta(
      importModuleInfo:frontInterface.ImportModuleInfo,
      mod:str, orgMod:str, baseDir:str!,
      loader:frontInterface.ModuleLoader ) mut: frontInterface.ModuleMeta!;
   pub fn loadFromLnsTxt( importModuleInfo:frontInterface.ImportModuleInfo,
                          baseDir:str!, name:str, txt:str ) mut : Luaval<&stem>;
   pub fn getLuaModulePath( mod:str, baseDir:str! ) mut : str, str!, str;
   pub fn searchModule( mod:str, baseDir:str!, addSearchPath:str! ) mut: str!;
   pub fn error( message:str ) __async;
}

fn Front.error( message:str ) __async {
   Util.errorLog( message );
   Util.printStackTrace();
   os.exit( 1 );
}

fn Front.loadLua( path:str ):Luaval<&stem>! {
   let ret:Luaval<&stem>!;
   __luaLock {
      let chunk, err = loadfile( path );
      when! chunk {
         ret = unwrap chunk(##);
      } else {
         Util.errorLog( unwrap err default "load error -- %s." (path));
         ret = nil;
      }
   }
   return ret;
}

fn createPaser( path:str, mod:str, stdinFile:&Types.StdinFile! ): Parser.Parser
{
   return Parser.StreamParser.create(
      Types.ParserSrc.LnsPath( path, mod, nil ), false, stdinFile, nil );
}

fn Front.scriptPath2Module( path:str ):str, str! {
   let mod = Util.scriptPath2ModuleFromProjDir( path, self.option.$projDir );
   return mod, self.option.$projDir;
}

fn Front.createPaser( scriptPath:str ): Parser.Parser
{
   let mod = self.scriptPath2Module( scriptPath );
   return createPaser( scriptPath, mod, self.option.$stdinFile );
}


alge CreateAstResult {
   Ast( &AstInfo.ASTInfo ),
   Creater(AstCreater),
}

fn getAstFromResult( result:&CreateAstResult ) __async : &AstInfo.ASTInfo {
   match result {
      case .Ast( ast ) {
         return ast;
      }
      case .Creater( creater ) {
         return (creater.getAst());
      }
   }
}

fn Front.createAstSub(
   importModuleInfo:frontInterface.ImportModuleInfo,
   parserSrc:&Types.ParserSrc,
   baseDir:str!, mod:str, moduleId:&frontInterface.ModuleId,
   analyzeModule:str!, analyzeMode:TransUnit.AnalyzeMode,
   pos:&Parser.Position! ) mut : CreateAstResult
{
   if! self.moduleMgr.get( mod ) {
      match _exp {
         case .Update( _, ast ) {
            return CreateAstResult.Ast( ast );
         }
         case .Uptodate( _ ) {
            Util.err( "can't load the multiple module -- %s" (mod) );
         }
      }
   }
   
   // let mut transUnit = new TransUnit.TransUnit(
   //    moduleId, importModuleInfo, new convLua.MacroEvalImp( self.builtinFunc ),
   //    analyzeModule, analyzeMode, pos, self.option.targetLuaVer,
   //    self.option.transCtrlInfo, self.builtinFunc );
   // let mut ast = transUnit.createAST( parserSrc, false, mod );

   if! let mut creater = self.mod2astCreate[ mod ] {
      let ast = creater.getAst();
      return CreateAstResult.Ast( ast );
   }

   let mut astCreater = new AstCreater(
      importModuleInfo, parserSrc, mod, baseDir, moduleId, analyzeModule,
      analyzeMode, pos, self.builtinFunc, self.option );
   self.mod2astCreate[ mod ] = astCreater;
   return CreateAstResult.Creater( astCreater );
}

fn Front.applyAstResult( result:&CreateAstResult ) mut : &AstInfo.ASTInfo {
   _match result {
      case .Ast( ast ) {
         return ast;
      }
      case .Creater( astCreater ) {
         let ast, moduleInfo, mut meta = astCreater.getAst();
         Log._log2( .Log, "applyAstResult -- %s" ( ast.$exportInfo.$fullName ));
         self.moduleMgr.add( ast, moduleInfo );
         self.moduleMgr.addMeta( ast.$exportInfo.$fullName, meta );
         
         self.mod2loader[ ast.$exportInfo.$fullName ] = nil;
         self.mod2astCreate[ ast.$exportInfo.$fullName ] = nil;
         
         return ast;
      }
   }
}



fn Front.createAst(
   importModuleInfo:frontInterface.ImportModuleInfo,
   parserSrc:&Types.ParserSrc,
   baseDir:str!, mod:str, moduleId:&frontInterface.ModuleId,
   analyzeModule:str!, analyzeMode:TransUnit.AnalyzeMode,
   pos:&Parser.Position! ) mut : &AstInfo.ASTInfo
{
   return self.applyAstResult( self.createAstSub(
      importModuleInfo, parserSrc, baseDir, mod, 
      moduleId, analyzeModule, analyzeMode, pos ) );
}


fn ast2LuaMain(
   ast:&AstInfo.ASTInfo, streamName:str, stream:oStream,
   metaStream:oStream, convMode:convLua.ConvMode,
   inMacro:bool, option:&Option.Option ) __async : convLua.FilterInfo
{
   let mut exportInfo = ast.$exportInfo;
   let mut conv = convLua.createFilter(
      streamName, stream, metaStream, convMode, inMacro,
      exportInfo.$moduleTypeInfo, exportInfo.$processInfo,
      exportInfo.$provideInfo.$symbolKind, ast.$builtinFunc,
      option.useLuneModule, option.targetLuaVer,
      option.testing, option.useIpairs );
   return conv;
}

fn ast2Lua(
   ast:&AstInfo.ASTInfo, streamName:str, stream:oStream, metaStream:oStream,
   convMode:convLua.ConvMode, inMacro:bool, option:&Option.Option )
{
   let mut conv = ast2LuaMain(
      ast, streamName, stream, metaStream, convMode, inMacro, option );
   conv.outputLuaAndMeta( unwrap ast.$node@@@Nodes.RootNode );
}

fn loadFromChunk( chunk:Luaval<form>!, err:str! ): Luaval<&stem>! {
   when! err {
      Util.errorLog( err );
   }
   let ret:Luaval<&stem>!;
   __luaLock {
      when! chunk {
         if! let mut work = chunk(##) {
            ret = work@@Luaval<stem>;
         } else {
            ret = nil;
         }
      } else {
         error( "failed to error" );
      }
   }
   return ret;
}

pub fn loadFromLuaTxt( txt:str ): Luaval<&stem>! {
   //--print( txt )
   let chunk, err;
   __luaLock {
      chunk, err = _load( txt ## );
   }
   return loadFromChunk( chunk, err );
}

fn byteCompileFromLuaTxt( txt:str, stripDebugInfo:bool ): str {
   let ret;
   __unsafe {
      let chunk, err = _load( txt ## );
      when! chunk {
         ret = string.dump( chunk, stripDebugInfo );
      } else {
         error( unwrap err default "load error" );
      }
   }
   return ret;
}


fn Front.convertFromAst(
   ast:&AstInfo.ASTInfo, streamName:str, mode:convLua.ConvMode ) : str, str
{
   let mut stream = new Util.memStream();
   let mut metaStream = new Util.memStream();

   ast2Lua( ast, streamName, stream, metaStream, mode, false, self.option );

   return metaStream.$txt, stream.$txt;
}

/**
マクロ解析用
*/
fn Front.loadFromLnsTxt(
   importModuleInfo:frontInterface.ImportModuleInfo,
   baseDir:str!, name:str, txt:str ) mut : Luaval<&stem>
{
   let mut transUnit = new TransUnit.TransUnit(
      frontInterface.ModuleId.tempId,
      importModuleInfo, new convLua.MacroEvalImp( self.builtinFunc ),
      false, nil, nil, nil,
      self.option.targetLuaVer, self.option.transCtrlInfo, self.builtinFunc );

   let mut ast = transUnit.createAST(
      Types.ParserSrc.LnsCode( txt, name, nil ), false, baseDir, self.option.$stdinFile,
      false, "$load%d" (self.loadCount), nil );
   self.loadCount = self.loadCount + 1;

   let _, luaTxt = self.convertFromAst( ast, name, .ConvMeta );
   return unwrap loadFromLuaTxt( luaTxt );
}

class DependMetaInfo extend (Mapping) {
   pub let use:bool;
   pub let buildId:str;
}

class MetaForBuildId extend (Mapping) {
   pub let __buildId:str;
   pub let __dependModuleMap: &Map<str,&DependMetaInfo>;
   pub let __subModuleMap: &List<str>;
   pub let __enableTest:bool;

   pub fn createModuleId(): &frontInterface.ModuleId {
      return frontInterface.ModuleId.createIdFromTxt( self.__buildId );
   }
}

pub static fn MetaForBuildId.LoadFromMeta( metaPath:str ): &MetaForBuildId!, str! {
   if! let mut fileObj = io.open( metaPath## ) {
      let luaCode = fileObj.read( "*a" );
      fileObj.close();
      when! luaCode {
         let meta;
         __unsafe {
            meta = MetaForBuildId._fromStem(
               expandLuavalMap( loadFromLuaTxt( luaCode )) );
         }
         return meta, luaCode;
      }
   }
   return nil, nil;
}


fn getMetaInfo( lnsPath:str, mod:str, outdir:str! ): &MetaForBuildId!, str, str {
   let mut moduleMetaPath = lnsPath;
   when! outdir {
      moduleMetaPath = "%s/%s" ( outdir , (mod.gsub( "%.", "/" )) );
   }
   moduleMetaPath = moduleMetaPath.gsub( "%.lns$", ".meta" );
   if! let meta, metaCode = MetaForBuildId.LoadFromMeta( moduleMetaPath ) {
      return meta, moduleMetaPath, metaCode;
   }
   return nil, moduleMetaPath, "";
}

fn Front.searchModuleFile( mod:str, suffix:str, baseDir:str!, outputDir:str! ) mut : str!
{
   _match self.gomodMap.convLocalModulePath( mod, suffix, baseDir ) {
      case .NotGo {
      }
      case .NotFound {
         return nil;
      }
      case .Found( info ) {
         return info.$path;
      }
   }
   
   let mut lnsSearchPath = package.path;
   when! outputDir {
      lnsSearchPath = string.format( "%s/?%s;%s", outputDir, suffix, package.path );
   }
   when! baseDir {
      lnsSearchPath = string.format( "%s/?%s;%s", baseDir, suffix, package.path );
   }
   if! let projDir = self.option.$projDir {
      lnsSearchPath = "%s;%s" ( Util.pathJoin( projDir, "?%s" (suffix)), package.path );
   }
   lnsSearchPath = lnsSearchPath.gsub( "%.lua$", suffix );
   lnsSearchPath = lnsSearchPath.gsub( "%.lua;", suffix .. ";" );

   let! foundPath = Depend.searchpath( mod, lnsSearchPath ) {
      // 通常パスで検索してない場合、
      // gomodMap でアクセスした最後の proj からの相対で見つける
      let! latestProjRoot = self.gomodMap.getLatestProjRoot() {
         return nil;
      };
      let latestProjSearchPath = Util.pathJoin( latestProjRoot, "?" .. suffix );
      if! Depend.searchpath( mod, latestProjSearchPath ) {
         foundPath = _exp;
      } else {
         Log._log2( .Err,
                    "not found '%s' at %s" (mod,latestProjSearchPath) );
         return nil;
      }
   };
   return (foundPath.gsub( "^./", "" ));
}

/**
指定の .lns ファイルのパス lnsPath の、ModuleId を取得する。

@param lnsPath .lns ファイルのパス
@param mod .lns ファイルのモジュール名
@param outdir .meta ファイルの出力先ディレクトリ。 nil の場合は .lns ファイルと同じ。
@param metaInfo .lns ファイルのメタ情報。 nil の場合は、この関数内で読み込む。
@return &frontInterface.ModuleId

lnsPath が存在しない場合、 frontInterface.ModuleId.tempId を返す。
メタ情報が有効な場合、 そのメタ情報に記載されている buildCount の ModuleId を返す。
メタ情報が無効な場合、 buildCount 0 の ModuleId を返す。
*/
fn getModuleId( lnsPath:str, mod:str, outdir:str!, mut metaInfo:&MetaForBuildId! ):
   &frontInterface.ModuleId
{
   let mut buildCount = 0;
   let! mut fileTime = Depend.getFileLastModifiedTime( lnsPath ) {
      return frontInterface.ModuleId.tempId;
   };
   if not metaInfo {
      metaInfo = getMetaInfo( lnsPath, mod, outdir );
   }
   when! metaInfo {
      let buildId = metaInfo.createModuleId();
      buildCount = buildId.$buildCount;
   }
   return frontInterface.ModuleId.createId( fileTime, buildCount );
}

alge ModuleUptodate {
   /** ファイルの更新が必要 */
   NeedUpdate,
   /** .meta ファイルの日付だけ更新が必要 */
   NeedTouch( metaCode:str, metaBuildId:&MetaForBuildId ),
   /** 最新で処理不要 */
   Uptodate( metaBuildId:&MetaForBuildId ),
}

/**
lnsPath で指定する lns ファイルを更新する必要があるかどうか確認する。

次の条件を全て満す時に uptodate として判断する。
- .lns ファイルに対応する .meta ファイルが存在し、更新日時が .meta の方が新しい
- .meta に記載の依存ファイルが存在し、更新日時に変更がない

@param lnsPath lns ファイルパス
@param mod lnsPath のモジュール
@return frontInterface.ModuleId lnsPath のモジュール ID
@return bool uptodate かどうか
@return &MetaForBuildId! lnsPath ファイルに対応するメタ情報
@return str メタ情報内容そのものの文字列
*/
fn Front.getModuleIdAndCheckUptodate( lnsPath:str, mod:str ) mut :
&frontInterface.ModuleId, ModuleUptodate
{
   let mut uptodate = ModuleUptodate.NeedUpdate;

   _match self.option.transCtrlInfo.uptodateMode {
      case .ForceAll {
         // 必ず NeedUpdate にする
         return frontInterface.ModuleId.tempId, uptodate;
      }
      case .Force1( forceMod ) {
         if mod == forceMod {
            return frontInterface.ModuleId.tempId, uptodate;
         }
      }
      case .Normal {
         // pass
      }
      case .Touch {
         // pass
      }
   }

   /**
dependModuleMap のモジュールのメタファイルが、 metaTime よりも古いかどうかを確認する。

metaTime
   */
   fn checkDependUptodate(
      metaTime:real, metaInfo:&MetaForBuildId, metaCode:str ): ModuleUptodate
   {
      foreach dependItem, depMod in metaInfo.__dependModuleMap {
         let! modMetaPath = self.searchModuleFile(
            depMod, ".meta", nil, self.option.outputDir )
         {
            // .meta ファイルがない場合、更新が必要
            Log._log( .Debug, `{ return "NeedUpdate"; } );
            return ModuleUptodate.NeedUpdate;
         };
         let! time = Depend.getFileLastModifiedTime( modMetaPath ) {
            // .meta ファイルの更新日時取得が出来ない場合、更新が必要
            Log._log( .Debug, `{ return "NeedUpdate"; } );
            return ModuleUptodate.NeedUpdate;
         };
         if time > metaTime {
            // インポート対象のモジュールのメタファイルの更新日時が
            // ビルド対象のモジュールのメタファイルよりも新しい場合、
            // .meta の内容を確認する。
            let! dependMeta = MetaForBuildId.LoadFromMeta( modMetaPath ) {
               Log._log( .Debug, `{ return "NeedUpdate"; } );
               return ModuleUptodate.NeedUpdate;
            };
            let orgMetaModuleId = frontInterface.ModuleId.createIdFromTxt(
               dependItem.buildId );
            let metaModuleId = dependMeta.createModuleId();
            if metaModuleId.$buildCount ~= 0 and
               metaModuleId.$buildCount ~= orgMetaModuleId.$buildCount
            {
               // buildCount が一致しない場合、更新する。
               Log._log( .Debug, `{
                  return "NeedUpdate: %s, %d, %d" ( modMetaPath,
                                                    metaModuleId.$buildCount,
                                                    orgMetaModuleId.$buildCount );
               } );
               return ModuleUptodate.NeedUpdate;
            }
         }
      }
      if self.option.transCtrlInfo.uptodateMode == .Touch {
         return ModuleUptodate.NeedTouch( metaCode, metaInfo );
      }
      return ModuleUptodate.Uptodate( metaInfo );
   }


   let metaInfo, metaPath, metaCode = getMetaInfo( lnsPath, mod, self.option.outputDir );

   when! metaInfo {
      // meta 情報があれば、 moduleId から更新済みかどうかを確認する
      if metaInfo.__enableTest == self.option.testing {
         let buildId = frontInterface.ModuleId.createIdFromTxt( metaInfo.__buildId );
         if buildId ~= frontInterface.ModuleId.tempId {
            let lnsTime = Depend.getFileLastModifiedTime( lnsPath );
            let metaTime = Depend.getFileLastModifiedTime( metaPath );
            when! lnsTime, metaTime {
               if lnsTime == buildId.$modTime {
                  // 更新済みならば、依存ファイルの状態を確認する
                  uptodate = checkDependUptodate( metaTime, metaInfo, metaCode );
               }
            }
         }
      }
      else {
         // テストコードを持つモジュールは、テストモードがあっていないものは更新が必要
      }
   }
   else {
      Log._log( .Debug, `{ return "not found meta"; } );
   }

   let moduleId = getModuleId( lnsPath, mod, self.option.outputDir, metaInfo );
   if moduleId == frontInterface.ModuleId.tempId {
      Util.err( "not found -- %s" ( lnsPath ) );
   }
   return moduleId, uptodate;
}

/**
指定 stream 内の LuneScript をコンパイルし、 Lua コードを取得する。
*/
pub fn Front.convertLns2LuaCode(
   importModuleInfo:frontInterface.ImportModuleInfo,
   parserSrc:&Types.ParserSrc, baseDir:str!, stream:iStream, streamName:str ) mut : str
{
   let mod = self.scriptPath2Module( streamName );
   let mut ast = self.createAst(
      importModuleInfo, parserSrc, baseDir, mod,
      frontInterface.ModuleId.createId( 0.0, 0 ), nil, .Compile ## );

   let _, luaTxt = self.convertFromAst( ast, streamName, .ConvMeta );

   return luaTxt;
}

fn Front.getGoAppName(): str {
   let! appName = self.option.appName {
      appName = self.gomodMap.$name;
   };
   return appName;
}

fn Front.createGoOption( scriptPath:str ) : &convGo.Option {
   let packageName;
   if! self.option.packageName {
      packageName = _exp;
   } else {
      if not scriptPath.find( "/" ## ) {
         // "/" がない場合は main
         packageName = "main";
      }
      else {
         let parentPath = scriptPath.gsub( "/[^/]+$", "" ).gsub( ".*/", "" );
         if #parentPath == 0 {
            // root ファイルの場合
            packageName = "main";
         } elseif parentPath == "." {
            packageName = "main";
         } elseif parentPath == ".." {
            packageName = "main";
         } else {
            packageName = parentPath.gsub( "[^%w]", "" );
         }
      }
   }
   return new convGo.Option(
      packageName, self.getGoAppName(), self.option.mainModule,
      self.option.$addEnvArg, self.option.$enableRunner );
}


/**
parser から Lns を AST 解析し、 meta 情報と変換後の Lua コード返す。

@return Luaval<&stem> meta 情報
@return str Lua コード
*/
fn Front.loadParserToLuaCode(
   importModuleInfo:frontInterface.ImportModuleInfo, parserSrc:&Types.ParserSrc,
   path:str, mod:str, baseDir:str! ) mut : frontInterface.ModuleMeta, str
{

   let moduleId = getModuleId( path, mod ## );
   let mut ast = self.createAst(
      importModuleInfo, parserSrc, baseDir, mod, moduleId, nil, .Compile, nil );
   

   let mut metaTxt, luaTxt = self.convertFromAst( ast, path, .ConvMeta );
   Log._log2( .Trace, "Meta = %s" (metaTxt) );


   if self.option.updateOnLoad {
      fn saveFile( suffix:str, txt:str, byteCompile:bool,
                   stripDebugInfo:bool, checkUpdate:bool ){
         let mut newpath = "";
         if! let dir = self.option.outputDir {
            newpath = "%s/%s%s" ( dir, mod.gsub( "%.", "/" ), suffix );
         }
         else {
            newpath = path.gsub( ".lns$", suffix );
         }
         let mut saveTxt = txt;
         if byteCompile {
            saveTxt = byteCompileFromLuaTxt( saveTxt, stripDebugInfo );
         }
         if not forceUpdateMeta and checkUpdate {
            if! let mut fileObj = io.open( newpath ## ) {
               let oldTxt = fileObj.read( "*a" );
               fileObj.close();
               if saveTxt == oldTxt {
                  // 同じ場合はセーブしない
                  return;
               }
            }
         }
         if! let mut fileObj = io.open( newpath, "w" ) {
            fileObj.write( saveTxt );
            fileObj.close();
         }
      }
      saveFile( ".lua", luaTxt, self.option.byteCompile,
                self.option.stripDebugInfo, false );
      saveFile( ".meta", metaTxt, self.option.byteCompile, true, true );

      switch self.option.convTo {
         case .Go {
            let mut memStream = new Util.memStream();
            let mut conv = convGo.createFilter(
               self.option.testing, path, memStream, ast,
               self.createGoOption( path ) );
            ast.$node.processFilter( conv, new convGo.Opt( ast.$node ) );
            saveFile( ".go", memStream.$txt, false, false, false );
         }
      }
   }

   // let mut meta = new frontInterface.ModuleMeta(
   //    unwrap loadFromLuaTxt( metaTxt ), path, createModuleInfo( ast, mod, moduleId ) );
   // let mut meta = new frontInterface.ModuleMeta(
   //    path, .Module( createModuleInfo( ast, mod, moduleId ) ) );
   return unwrap self.moduleMgr.getMeta(mod), luaTxt;
}


/**
指定 path の LuneScript をロードし、
 meta 情報と変換した Lua コードをロードして実行した結果のオブジェクトを返す。

*/
fn Front.loadFile(
   importModuleInfo:frontInterface.ImportModuleInfo,
   baseDir:str!, path:str, mod:str ) mut : frontInterface.ModuleMeta, Luaval<&stem>
{
   Log._log2( .Info, "start %s:%s" ( __func__, mod ) );
   
   let mut meta, luaTxt = self.loadParserToLuaCode(
      importModuleInfo, Types.ParserSrc.LnsPath( path, mod, nil ), path, mod, baseDir )**;
   
   if! let mut preLoadInfo = self.preloadedModMap[ mod ] {
      return meta, preLoadInfo;
   }
   return meta, unwrap loadFromLuaTxt( luaTxt );
}



fn Front.searchModule( mod:str, baseDir:str!, addSearchPath:str! ) mut : str! {
   return self.searchModuleFile( mod, ".lns", baseDir, addSearchPath );
}

fn Front.searchLuaFile(
   moduleFullName:str, baseDir:str!, addSearchPath:str! ) mut : str!
{
   return self.searchModuleFile( moduleFullName, ".lua", baseDir, addSearchPath );
}

/**
指定のメタファイルが最新で更新が必要ないか調べる。

@param metaPath メタファイルのパス
@return stem! 最新の場合、ロードしたメタ情報。 最新でない場合は nil。
*/
fn Front.checkUptodateMeta(
   lnsPath:str, metaPath:str,
   baseDir:str!, addSearchPath:str! ) mut : frontInterface.ModuleMeta!
{
   let! metaObj = self.loadLua( metaPath ) {
      Log._log( .Warn, `{ return "load error -- %s" ( metaPath ); } );
      return nil;
   };

   let meta;
   let mut valid = true;
   __luaLock {
      meta = metaObj@@=Meta._MetaInfo;
      if meta.$__formatVersion ~= Ver.metaVersion {
         Log._log( .Warn, `{ return "unmatch meta version -- %s" ( metaPath ); } );
         valid = false;
      }
      if valid and meta.$__hasTest {
         // テストコードを持つモジュールは、テストモードがあっていないものは更新が必要
         if meta.$__enableTest ~= self.option.testing {
            Log._log( .Warn, `{ return "unmatch test setting -- %s" ( metaPath ); } );
            valid = false;
         }
      }
      if valid {
         foreach _, moduleFullName in meta.__dependModuleMap {
            if! let moduleLnsPath =
               self.searchModule( moduleFullName, baseDir, addSearchPath )
            {
               if! let moduleLuaPath = self.searchLuaFile(
                  moduleFullName, baseDir, addSearchPath ) {
                     if not Util.getReadyCode( moduleLnsPath, metaPath ) {
                        // 依存するモジュールが更新されている場合、
                        // 作成済みのメタファイルは使えないので nil を返す。
                        Log._log( .Warn, `{ return "not ready -- %s, %s"
                                            ( moduleLnsPath, metaPath ); } );
                        valid = false;
                        break;
                     }
                     let moduleMetaPath = moduleLuaPath.gsub( "%.lua$", ".meta" );
                     if Depend.existFile( moduleMetaPath ) and
                        not Util.getReadyCode( moduleMetaPath, metaPath )
                     {
                        Log._log( .Warn, `{ return "not ready -- %s, %s"
                                            ( moduleMetaPath, metaPath ); } );
                        valid = false;
                        break;
                     }
                  } else {
                     Log._log( .Warn, `{ return "not found .lua file for -- %s"
                                         ( moduleFullName ); } );
                     valid = false;
                     break;
                  }
            } else {
               Log._log( .Warn,
                         `{ return "not found .lns file -- %s" ( moduleFullName); } );
               valid = false;
               break;
            }
         }
      }
   }
   if not valid {
      return nil;
   }
   return new frontInterface.ModuleMeta( lnsPath, .MetaRaw( meta ) );
}

/**
mod のモジュールをロードする。

@return Luaval<&stem>! モジュール情報
@return Luaval<&stem> META 情報
*/
fn Front.loadModule( mut mod:str ) __async mut : Luaval<&stem>!, frontInterface.ModuleMeta {
   let orgMod = mod;
   let baseDir:str! = nil;
   _, _, mod = self.gomodMap.getLuaModulePath( mod, baseDir );
   
   if not self.getLoadInfo( mod ) {
      if! let luaTxt = self.convertedMap[ mod ] {
         if! let mut meta = self.moduleMgr.getMeta( mod ) {
            self.setLoadInfo(
               mod, new LoadInfo( unwrap loadFromLuaTxt( luaTxt ), meta ) );
         }
         else {
            error( "nothing meta -- %s" ( mod ) );
         }
      }
      else {
         if! let lnsPath = self.searchModule( orgMod, baseDir, nil ) {
            let mut luaPath:str! = string.gsub( lnsPath, "%.lns$", ".lua" );

            if! let dir = self.option.outputDir {
               luaPath = self.searchLuaFile( orgMod, nil, dir );
            }

            let mut loadVal:Luaval<&stem>! = nil;
            when! luaPath {
               if Util.getReadyCode( lnsPath, luaPath ) {
	          let metaPath = string.gsub( luaPath, "%.lua$", ".meta" );
	          if Util.getReadyCode( lnsPath, metaPath ) {
                     if! let preLoadInfo = self.preloadedModMap[ mod ] {
                        loadVal = preLoadInfo;
                     }
                     else {
	                loadVal = self.loadLua( luaPath );
                     }
                     if! loadVal {
                        if! let mut meta = self.checkUptodateMeta(
                           lnsPath, metaPath, baseDir, self.option.outputDir )    
                        {
	                   self.setLoadInfo( mod, new LoadInfo( _exp, meta ) );
                        }
                        else {
                           loadVal = nil;
                        }
                     }
                  }
               }
            }
            if loadVal == nil {
	       let mut meta, mut workVal = self.loadFile(
                  new frontInterface.ImportModuleInfo(), baseDir, lnsPath, mod );
	       self.setLoadInfo( mod, new LoadInfo( workVal, meta ) );
            }
         } else {
            // lns ファイルがない場合、 binding からロードする。
            if self.bindModuleSet.has( mod ) {
               Log._log2( .Warn, "load from the binding -- %s" (mod) );
               // require( mod ) することで、 bind しているモジュールをロードする。
               let workMod;

               __luaLock {
                  workMod = require( mod );
               }
               // meta は使用しないのでダミーで生成する。
               // 将来 meta を利用することになった場合は、修正が必要。             
               let mut meta = new frontInterface.ModuleMeta(
                  mod.gsub( "%.", "/" ) .. ".lns",
                  .MetaRaw( unwrap loadFromLuaTxt( "return {}" ) ) );
	       self.setLoadInfo( mod, new LoadInfo( workMod, meta ) );
            }
         }
      }
   }
   if! self.getLoadInfo( mod ) {
      //TestCtrl.add( mod, _exp.mod );
      return _exp.mod, _exp.meta;
   }
   error( "load error, %s" ( mod ) );
}


pub fn Front.getLuaModulePath( mod:str, baseDir:str! ) mut : str, str!, str {
   return self.gomodMap.getLuaModulePath( mod, baseDir )**;
}

fn Front.loadMeta(
   importModuleInfo:frontInterface.ImportModuleInfo, mod:str, orgMod:str,
   baseDir:str!, loader:frontInterface.ModuleLoader ) mut : frontInterface.ModuleMeta!
{
   if! let mut creater = self.mod2astCreate[ orgMod ] {
      let! exportInfo = creater.getExportInfo() {
         return nil;
      };
      let mut meta = new frontInterface.ModuleMeta(
         exportInfo.$streamName, .Export( exportInfo ));
      self.moduleMgr.addMeta( orgMod, meta );
   }

   if! let mut workLoader = self.mod2loader[ orgMod ] {
      let! exportInfo = workLoader.getExportInfo() {
         return nil;
      };
      let mut meta = new frontInterface.ModuleMeta(
         exportInfo.$streamName, .Export( exportInfo ));
      self.moduleMgr.addMeta( orgMod, meta );

      // // self.mod2loader をクリアするのは self.moduleMgr.addMeta() の後にする。
      // // こうしないと、self.moduleMgr.getMeta() に反映されないので、
      // // 再度 .meta から読み込んでしまう。
      // self.mod2loader[ orgMod ] = nil;
      return meta;
   }

   
   if not self.moduleMgr.getMeta( orgMod ) {
      self.mod2loader[ orgMod ] = loader;
      
      if! self.getLoadInfo( orgMod ) {
         // ロード済みのものを使用する
         self.moduleMgr.addMeta( orgMod, _exp.meta );
      } else {
         if! let lnsPath = self.searchModule( mod, baseDir, nil ) {
            let mut meta:frontInterface.ModuleMeta! = nil;

            if not self.targetSet.has( orgMod ) {
               let mut luaPath:str! = string.gsub( lnsPath, "%.lns$", ".lua" );
               if not baseDir {
                  if! let dir = self.option.outputDir {
                     luaPath = self.searchLuaFile( orgMod, nil, dir );
                  }
               }
               when! luaPath {
                  // .lua, .meta ファイルが更新済みか調べ
                  // 更新済みならそれをロードする
                  
                  let forceFlag;
                  _match self.option.transCtrlInfo.uptodateMode {
                     case .ForceAll {
                        forceFlag = true;
                     }
                     case .Force1( forceMod ) {
                        forceFlag = orgMod == forceMod;
                     }
                     case .Normal {
                        forceFlag = false;
                     }
                     case .Touch {
                        forceFlag = false;
                     }
                  }
                  if not forceFlag {
                     if Util.getReadyCode( lnsPath, luaPath ) {
	                let metaPath = string.gsub( luaPath, "%.lua$", ".meta" );
	                if Util.getReadyCode( lnsPath, metaPath ) {
                           meta = self.checkUptodateMeta(
                              lnsPath, metaPath, baseDir, self.option.outputDir );
                        } else {
                           Log._log( .Warn, `{ return "%s not ready meta %s, %s"
                                               ( orgMod, lnsPath, metaPath ); } );
                        }
                     } else {
                        Log._log( .Warn, `{ return "%s not ready lua %s, %s"
                                            ( orgMod, lnsPath, luaPath ); } );
                     }
                  } else {
                     Log._log2( .Warn, "force analyze -- %s" ( orgMod ) );
                  }
               }
               else {
                  Log._log( .Warn, `{ return "%s not found lua in %s"
                                      ( orgMod, self.option.outputDir ); } );
               }
            }

            when! meta {
	       self.moduleMgr.addMeta( orgMod, meta );
            }
            else {
	       let mut metawork, luaTxt = self.loadParserToLuaCode(
                  importModuleInfo,
                  Types.ParserSrc.LnsPath( lnsPath, orgMod, nil ),
                  lnsPath, orgMod, baseDir );
	       self.moduleMgr.addMeta( orgMod, metawork );
               self.convertedMap[ orgMod ] = luaTxt;
            }
         } else {
            if! let lnsCode = Depend.getBindLns( orgMod ) {
               let path = mod.gsub( "%.", "/" ) .. ".lns";
               let mut meta, luaTxt = self.loadParserToLuaCode(
                  importModuleInfo, 
                  Types.ParserSrc.LnsCode( lnsCode, orgMod, nil ),
                  path, orgMod, baseDir );
	       self.moduleMgr.addMeta( orgMod, meta );
               self.convertedMap[ orgMod ] = luaTxt;
            }
         }
      }
   }

   if! let mut meta = self.moduleMgr.getMeta( orgMod ) {
      return meta;
   }
   return nil;
}

pub fn Front.dumpTokenize( scriptPath:str ) mut {
   //frontInterface.setFront( self );

   let mut parser = self.createPaser( scriptPath );
   while true {
      let! token = parser.getToken() {
         break;
      };
      print( token.kind.$_txt, token.pos.lineNo, token.pos.column, token.txt );
   }
}

pub fn Front.dumpAst( scriptPath:str ) mut {
   //frontInterface.setFront( self );

   let mod, baseDir = self.scriptPath2Module( scriptPath );
   Depend.profile(
      self.option.validProf,
      fn() {
	 let mut ast = self.createAst(
            new frontInterface.ImportModuleInfo(),
            Types.ParserSrc.LnsPath( scriptPath, mod, nil ), baseDir,
            mod, getModuleId( scriptPath, mod ## ),
            nil, .Compile ## );
	 ast.$node.processFilter(
            dumpNode.createFilter(
               ast.$exportInfo.$moduleTypeInfo,
               ast.$exportInfo.$processInfo, io.stdout ),
            new dumpNode.Opt( "", 0 ) );
      }, scriptPath .. ".profi" );
}

pub fn Front.format( scriptPath:str ) mut {
   //frontInterface.setFront( self );

   let mod, baseDir = self.scriptPath2Module( scriptPath );

   let mut ast = self.createAst(
      new frontInterface.ImportModuleInfo(),
      Types.ParserSrc.LnsPath( scriptPath, mod, nil ), baseDir,
      mod, getModuleId( scriptPath, mod ## ),
      nil, .Compile ## );
   ast.$node.processFilter(
      Formatter.createFilter(
         ast.$exportInfo.$moduleTypeInfo, io.stdout ),
      new Formatter.Opt( ast.$node ) );
}

pub fn Front.checkDiag( scriptPath:str ) mut {
   //frontInterface.setFront( self );

   let mod, baseDir = self.scriptPath2Module( scriptPath );
   Util.setErrorCode( 0 );
   self.createAst( new frontInterface.ImportModuleInfo(),
                   Types.ParserSrc.LnsPath( scriptPath, mod, nil ), baseDir, mod,
                   getModuleId( scriptPath, mod ## ), nil, .Diag ## );
}

pub fn Front.complete( scriptPath:str ) mut {
   let mod, baseDir = self.scriptPath2Module( scriptPath );
   self.createAst( new frontInterface.ImportModuleInfo(),
                   Types.ParserSrc.LnsPath( scriptPath, mod, nil ), baseDir, mod,
                   getModuleId( scriptPath, mod ## ),
                   self.option.analyzeModule, .Complete, self.option.analyzePos );
}

pub fn Front.inquire( scriptPath:str ) mut {
   let mod, baseDir = self.scriptPath2Module( scriptPath );
   self.createAst( new frontInterface.ImportModuleInfo(),
                   Types.ParserSrc.LnsPath( scriptPath, mod, nil ), baseDir, mod,
                   getModuleId( scriptPath, mod ## ),
                   self.option.analyzeModule, .Inquire, self.option.analyzePos );
}


pub fn Front.createGlue(scriptPath:str) mut {
   //frontInterface.setFront( self );

   let mod, baseDir = self.scriptPath2Module( scriptPath );
   let mut ast = self.createAst(
      new frontInterface.ImportModuleInfo(),
      Types.ParserSrc.LnsPath( scriptPath, mod, nil ), baseDir, mod,
      getModuleId( scriptPath, mod ## ), nil, .Compile ## );
   let mut filter = glueFilter.createFilter( self.option.outputDir );
   ast.$node.processFilter( filter, 0 );
}

fn closeStreams( stream:oStream!, metaStream:Util.memStream!,
                 dependStream:oStream!, metaPath:str, saveMetaFlag:bool )
{
   fn txt2ModuleId( txt:str ) : frontInterface.ModuleId {
      let mut buildIdTxt = txt.gsub( "^_moduleObj.__buildId = ", "" ).gsub( '"', "" );
      return frontInterface.ModuleId.createIdFromTxt( buildIdTxt );
   }

   /**
oldStream, newStream に格納されているモジュールのメタ情報が、
モジュールのビルド ID 情報以外が一致しているかどうかを調べる。

モジュールのビルド ID 情報以外が一致している場合、
モジュールのビルド ID 情報の部分だけを更新したメタ情報を返す。

@param oldStream 古いメタ情報
@param newStream 新しいメタ情報
@return bool 一致している場合 true。 true の場合、次の str の情報が有効。
@return str 新しく書き込むメタ情報の文字列。
   */
   fn checkDiff( oldStream:Parser.TxtStream, newStream:Parser.TxtStream ) : bool, str
   {
      macro _getBuildId( line:__exp, buildIdLine:__exp ) {
         when! ,,line {
            if #,,buildIdLine == 0 {
               if ,,line.find( "^_moduleObj.__buildId" ## ) {
                  // ビルド ID 情報を取得する。
                  ,,buildIdLine = ,,line;
               }
            }
         }
      }


      // ビルド ID よりも前のファイル内部の位置
      let mut headEndPos = 0;
      // ビルド ID よりも後のファイル内部の位置
      let mut tailBeginPos = 0;

      // 古い build ID を記録した行
      let mut oldBuildIdLine = "";
      // 新しい build ID を記録した行
      let mut newBuildIdLine = "";
      while true {
         let newLine = newStream.read( "*l" );
         let oldLine = oldStream.read( "*l" );
         _getBuildId( oldLine, oldBuildIdLine );
         _getBuildId( newLine, newBuildIdLine );

         if newLine ~= oldLine {
            // 差分がある箇所を見つけて、それが ビルド ID の場合、
            // 
            let mut cont = false;
            when! newLine, oldLine {
               if oldLine.find( "^_moduleObj.__buildId" ## ) {
                  if newLine.find( "^_moduleObj.__buildId" ## ) {
                     tailBeginPos = newStream.$lineNo;
                     cont = true;
                  }
               }
               elseif oldLine.find( "^__dependModuleMap.*buildId =" ## ) and
                  newLine.find( "^__dependModuleMap.*buildId =" ## )
               {
                  let oldSub = oldLine.gsub( "buildId =.*", "" );
                  let newSub = newLine.gsub( "buildId =.*", "" );
                  if oldSub == newSub {
                     cont = true;
                  }
               }
            }
            if not cont {
               Log._log( .Debug, `{ return "<%s>, <%s>" (oldLine, newLine ); } );
               return false, "";
            }
         }
         else {
            if tailBeginPos == 0 {
               headEndPos = newStream.$lineNo;
            }
            if not oldLine {
               // ファイル一致
               if tailBeginPos == 0 {
                  return true, oldStream.$txt;
               }

               let oldBuildId = txt2ModuleId( oldBuildIdLine );
               let newBuildId = txt2ModuleId( newBuildIdLine );
               let worlBuildId = frontInterface.ModuleId.createId(
                  newBuildId.$modTime, oldBuildId.$buildCount );
               let buildIdLine = "_moduleObj.__buildId = %q" (worlBuildId.$idStr);

               let txt = "%s%s\n%s" ( newStream.getSubstring( 1, headEndPos ),
                                      buildIdLine,
                                      newStream.getSubstring( tailBeginPos## ) );
               return true, txt;
            }
         }
      }
   }

   
   when! stream {
      stream.close();
   }
   when! dependStream {
      dependStream.close();
   }

   when! metaStream {
      if saveMetaFlag {
         // メタファイルが変っているかどうかを確認し、
         // 変更がない場合はビルド情報だけ更新する。
         let newMetaTxt = metaStream.$txt;
         let mut oldMetaTxt = "";
         if! let mut oldFileObj = io.open( metaPath ## ) {
            oldMetaTxt = unwrap oldFileObj.read( "*a" ) default "";
            oldFileObj.close();
         }

         let sameFlag, txt = checkDiff( new Parser.TxtStream( oldMetaTxt ),
                                        new Parser.TxtStream( newMetaTxt ) );

         fn saveMeta( meta:str ) {
            if! let mut fileObj = io.open( metaPath, "w" ) {
               fileObj.write( meta );
               fileObj.close();
            } else {
               Util.err( "failed to open -- %s" (metaPath) );
            }
         }
         
         if not sameFlag {
            saveMeta( newMetaTxt );
         }
         else {        
            if txt ~= "" {
               saveMeta( txt );
            }
         }
      }
   }
}

class LuaConverter extend Runner.Runner {
   let luaPath:str;
   let dependsPath:str!;
   let metaPath:str;
   let option:&Option.Option;
   let byteCompile:bool;
   let stripDebugInfo:bool;
   let byteStream:Util.memStream;
   let byteMetaStream:Util.memStream;
   let streamMem:Util.memStream;
   let metaStreamMem:Util.memStream;
   let dependsStreamMem:Util.memStream;
   let astResult:&CreateAstResult;

   let converterFunc:ConverterFunc;
   let mut filterInfo:convLua.FilterInfo!;

   pub fn __init(
      luaPath:str, metaPath:str, dependsPath:str!, 
      astResult:&CreateAstResult, convMode:convLua.ConvMode,
      path:str, byteCompile:bool,
      stripDebugInfo:bool, option:&Option.Option ) __async
   {
      super();
      
      self.luaPath = luaPath;
      self.metaPath = metaPath;
      self.dependsPath = dependsPath;
      self.option = option;
      self.stripDebugInfo = stripDebugInfo;
      self.byteCompile = byteCompile;
      self.byteStream = new Util.memStream();
      self.byteMetaStream = new Util.memStream();
      self.streamMem = new Util.memStream();
      self.metaStreamMem = new Util.memStream();
      self.dependsStreamMem = new Util.memStream();
      self.astResult = astResult;
      self.filterInfo = nil;

      self.converterFunc = fn() __async {
         // 一旦メモリに出力するためのストリーム作成
         let mut stream = self.streamMem;
         let mut metaStream = self.metaStreamMem;
         
         let mut outStream, mut oMetaStream = stream, metaStream;

         let ast = getAstFromResult( self.astResult );
         
         let needDepends = option.dependsPath ~= nil;
         if needDepends {
            ast.$node.processFilter(
               OutputDepend.createFilter( self.dependsStreamMem ), 1 );
         }

         if byteCompile {
            // byteCompile 時は、出力先をメモリに切り替える
            outStream = self.byteStream;
            oMetaStream = self.byteMetaStream;
         }

         let mut filterInfo = ast2LuaMain(
            ast, path, outStream, oMetaStream, convMode, false, option );
         self.filterInfo = filterInfo;
         filterInfo.outputLua( unwrap ast.$node@@@Nodes.RootNode );
      };
      self.start( __lns.runMode.Queue, "convlua -- %s" (path) );
   }

   pro override fn runMain() __async mut {
      self.converterFunc();
   }
   
   pub fn saveLua() mut {
      __join( self );

      let ast = getAstFromResult( self.astResult );
      self.filterInfo$.outputMeta$( unwrap ast.$node@@@Nodes.RootNode );
      
      
      if self.byteCompile {
         // byteCompile 時は、
         // メモリに出力したコードをバイトコンパイルしてセーブする
         self.streamMem.write(
            byteCompileFromLuaTxt( self.byteStream.$txt, self.stripDebugInfo ) );
         self.metaStreamMem.write(
            byteCompileFromLuaTxt( self.byteMetaStream.$txt, true ) );
      }

      // メモリに出力したものから本来出力先に
      let luaCode = self.streamMem.$txt;
      let metaTxt = self.metaStreamMem.$txt;
      let dependTxt:str!;
      let needDepends = self.option.dependsPath ~= nil;
      if needDepends {
         dependTxt = self.dependsStreamMem.$txt;
      } else {
         dependTxt = nil;
      }
      
      let! mut streamDst = io.open( self.luaPath, "w" ) {
         error( "write open error -- %s" (self.luaPath) );
      };
      let mut dependsStreamDst = self.option.openDepend( self.dependsPath );
      

      streamDst.write( luaCode );
      let mut metaMemStream = new Util.memStream();
      metaMemStream.write( metaTxt );
      when! dependsStreamDst {
         dependsStreamDst.write( unwrap dependTxt );
      }
      
      closeStreams( streamDst, metaMemStream, dependsStreamDst,
                    self.metaPath, self.option.mode == .SaveMeta );
   }
}

fn outputDependInfo( stream:oStream!, metaInfo:&MetaForBuildId, mod:str ) {
   when! stream {
      let mut dependInfo = new OutputDepend.DependInfo( mod );
      foreach _, dependMod in metaInfo.__dependModuleMap {
         dependInfo.addImpotModule( dependMod );
      }
      foreach subMod in metaInfo.__subModuleMap {
         dependInfo.addSubMod( subMod );
      }
      dependInfo.output( stream );
   }
}

fn Front.convertToLua(
   scriptPath:str, convMode:convLua.ConvMode,
   streamLua:oStream, streamMeta:oStream ) mut : &AstInfo.ASTInfo!
{
   let mod, baseDir = self.scriptPath2Module( scriptPath );

   let moduleId = getModuleId( scriptPath, mod ## );
   let mut ast = self.createAst(
      new frontInterface.ImportModuleInfo(),
      Types.ParserSrc.LnsPath( scriptPath, mod, nil ), baseDir,
      mod, moduleId, nil, .Compile, nil );
   let mut metaTxt, luaTxt = self.convertFromAst( ast, scriptPath, convMode );
   streamLua.write( luaTxt );
   streamMeta.write( metaTxt );
   
   switch self.option.convTo {
      case .Go {
         let mut conv = convGo.createFilter(
            self.option.testing, "stdout", streamLua, ast,
            self.createGoOption( scriptPath ) );
         ast.$node.processFilter( conv, new convGo.Opt( ast.$node ) );
      }
   }

   return ast;
}

class GoConverter extend Runner.Runner {
   let mut memStream:Util.memStream;
   let path:str;
   let converter:ConverterFunc;
   let mut validFlag:bool;
   
   local fn __init( scriptPath:str, astResult:&CreateAstResult,
                    option:&Option.Option, goOpt:&convGo.Option ) __async
   {
      super();
      self.validFlag = true;
      
      let mut path = scriptPath.gsub( "%.lns$", ".go" );

      if! let dir = option.outputDir {
         path = "%s/%s" (dir,path);
      }

      self.path = path;
      self.memStream = new Util.memStream();

      self.converter = fn () {
         let ast = getAstFromResult( astResult );

         let! rootNode = ast.$node@@@Nodes.RootNode {
            return;
         };
         foreach pragma in rootNode.$luneHelperInfo.pragmaSet {
            match pragma {
               case .limit_conv_code( codeSet ) {
                  if not codeSet.has( LuneControl.Code.Go ) {
                     self.validFlag = false;
                     return;
                  }
               }
            }
         }

         
         let mut conv = convGo.createFilter(
            option.testing, path, self.memStream, ast, goOpt );
         ast.$node.processFilter( conv, new convGo.Opt( ast.$node ) );
      };
      self.start( __lns.runMode.Queue, "convgo -- %s" (scriptPath) );
   }

   pro override fn runMain() __async mut {
      self.converter();
   }

   local fn saveGo() mut {
      __join( self );

      if not self.validFlag {
         return;
      }
      
      
      let! mut file = io.open( self.path, "w" ) {
         return;
      };
      
      file.write( self.memStream.$txt );

      file.close();
   }
}


/**
Go に変換しファイルに書き込む
*/
local fn Front.saveToGo( scriptPath:str, astResult:&CreateAstResult ) mut : GoConverter! {
   return new GoConverter(
      scriptPath, astResult, self.option, self.createGoOption( scriptPath ) );
}


/**
C に変換しファイルに書き込む
*/
pub fn Front.saveToC( scriptPath:str, ast:&AstInfo.ASTInfo ) mut {
   let mut cPath = scriptPath.gsub( "%.lns$", ".c" );

   let! mut file = io.open( cPath, "w" ) {
      return;
   };

   let mut hPath = scriptPath.gsub( "%.lns$", ".h" );
   let! mut hFile = io.open( hPath, "w" ) {
      return;
   };

   file.close();
   hFile.close();
}

pub fn Front.outputBuiltin( scriptPath:str ) mut {
   let mod, baseDir = self.scriptPath2Module( "lns_builtin" );

   let mut ast = self.createAst(
      new frontInterface.ImportModuleInfo(),
      Types.ParserSrc.LnsCode( "", mod, nil ), baseDir, mod,
      frontInterface.ModuleId.createId( 0.0, 0 ), nil, .Compile ## );

   self.saveToC( scriptPath, ast );
}

class UpdateInfo {
   let scriptPath:str {pub};
   let dependsPath:str! {pub};
   //let mut parser:Parser.Parser! {pub};
   let moduleId:&frontInterface.ModuleId {pub};
   let uptodate: ModuleUptodate {pub};
}

/**
Lua に変換しファイルに書き込む
*/
fn Front.saveToLua( updateInfo:UpdateInfo ) mut : ConverterFunc!
{
   let scriptPath = updateInfo.$scriptPath;
   let dependsPath = updateInfo.$dependsPath;
   //let mut parser = updateInfo.$parser;
   let moduleId = updateInfo.$moduleId;
   let uptodate = updateInfo.$uptodate;
   

   let mod, baseDir = self.scriptPath2Module( scriptPath );
   let mut luaPath = scriptPath.gsub( "%.lns$", ".lua" );
   let mut metaPath = scriptPath.gsub( "%.lns$", ".meta" );
   if self.option.outputDir {
      let filename = mod.gsub( "%.", "/" );
      luaPath = "%s/%s.lua" ( self.option.outputDir, filename );
      metaPath = "%s/%s.meta" ( self.option.outputDir, filename );
   }

   // Meta 出力モードで変換する。
   // Meta は memStream 上に出力し、不要な場合は保存せずに破棄する。
   let convMode = convLua.ConvMode.ConvMeta;



   if luaPath == scriptPath {
      Util.errorLog( "%s is illegal filename." (luaPath) );
      return nil;
   }
   
   // .meta ファイルを更新すると、このモジュールを import している
   // モジュール全てを更新する必要がある。
   // しかし、この更新処理は無駄になることが多い。
   // 例えば、モジュール内の local な関数の挙動を変更しただけならば、
   // このモジュールを import しているモジュールには影響がないので、
   // 更新の必要はない。
   // このような無駄な更新処理をスキップできるように、
   // .meta 情報に変更がない場合は、外部モジュールの更新が行なわれないように
   // 制御を行なう。


   _match uptodate {
      case .NeedUpdate {
         let mut result = self.createAstSub(
            new frontInterface.ImportModuleInfo(),
            Types.ParserSrc.LnsPath( scriptPath, mod, nil ), baseDir,
            mod, moduleId, nil, .Compile, ## );

         let mut luaConv = new LuaConverter(
            luaPath, metaPath, dependsPath,
            result, convMode, scriptPath, self.option.byteCompile,
            self.option.stripDebugInfo, self.option );

         let mut goConv:GoConverter! = nil;
         switch self.option.convTo {
            // case .C {
            //    self.saveToC( scriptPath,  );
            // }
            case .Go {
               goConv = self.saveToGo( scriptPath, result );
            }
         }
         return fn () __noasync {
            luaConv.saveLua();
            goConv$.saveGo$();
         };
      }
      case .Uptodate( metaInfo ) {
         Util.errorLog( "uptodate -- " .. scriptPath );

         let mut dependsStream = self.option.openDepend( dependsPath );
         outputDependInfo( dependsStream, metaInfo, mod );
         closeStreams( nil, nil, dependsStream, metaPath, false );

         return nil;
      }
      case .NeedTouch( metaCode, metaInfo ) {
         // Lns は解析せずにメタファイルの更新日時だけ更新する。
         Util.errorLog( "touch -- " .. scriptPath );

         let mut dependsStream = self.option.openDepend( dependsPath );
         let mut metaMemStream = new Util.memStream();
         if self.option.mode == .SaveMeta {
            metaMemStream.write( metaCode );
         }
         outputDependInfo( dependsStream, metaInfo, mod );
         closeStreams( nil, metaMemStream, dependsStream,
                       metaPath, self.option.mode == .SaveMeta );
         return nil;
      }
   }
}


fn Front.outputBootC( scriptPath:str )
{
   // let mut stream:oStream;
   // if! let path = self.option.bootPath {
   //    let cPath = path.gsub( "%.lns$", ".c" );
   //    let! mut file = io.open( cPath, "w" ) {
   //       print( "failed to open file -- %s" (cPath) );
   //       return;
   //    };
   //    stream = file;
   // }
   // else {
   //    stream = io.stdout;
   // }
   // let initModule = scriptPath2Module( scriptPath );
   // convCC.outputBootcode( stream, initModule );
}

pub fn convertLnsCode2LuaCode( lnsCode:str, path:str, baseDir:str! ) __noasync : str
{
   let mut option = new Option.Option();
   option.scriptPath = path;
   option.useLuneModule = Option.getRuntimeModule();
   option.useIpairs = true;
   let mut front = new Front( option ## );
   
   return front.convertLns2LuaCode(
      new frontInterface.ImportModuleInfo(),
      Types.ParserSrc.LnsCode( lnsCode, path, nil ), baseDir,
      new Parser.TxtStream( lnsCode ), path );
}

alge BuildMode {
   Save,
   CreateAst,
   Output(streamLua:oStream, streamMeta:oStream),
}

pub fn Front.build( buildMode:BuildMode, astCallback:AstCallback! ) mut {
   fn createUpdateInfo( scriptPath:str, dependsPath:str! ) : UpdateInfo {
      let mod = self.scriptPath2Module( scriptPath );
      let moduleId, uptodate = self.getModuleIdAndCheckUptodate( scriptPath, mod );
      return new UpdateInfo( scriptPath, dependsPath, moduleId, uptodate );
   }

   fn process( oneShot:bool, updateInfo:UpdateInfo ) __trans : ConverterFunc! {
      let mod, baseDir = self.scriptPath2Module( updateInfo.$scriptPath );
      
      _match buildMode {
         case .Save {
            return self.saveToLua( updateInfo );
         }
         case .Output( streamLua, streamMeta ) {
            self.convertToLua(
               updateInfo.$scriptPath, .ConvMeta, streamLua, streamMeta );       
         }
         case .CreateAst {
            if not self.mod2astCreate[ mod ] and not self.moduleMgr.getAst( mod ) {     
               let mut result = self.createAstSub(
                  new frontInterface.ImportModuleInfo(),
                  Types.ParserSrc.LnsPath( updateInfo.$scriptPath, mod, nil ), baseDir,
                  mod, updateInfo.$moduleId, nil, .Compile, ## );
               return fn () __noasync {
                  self.applyAstResult( result );
               };
            }
         }
      }
      return nil;
   }

   Depend.profile(
      self.option.validProf,
      fn () {
         if self.option.scriptPath == "@-" {
            // pending: batch モードの場合は forceAll にする。
            foreach path in self.option.batchList {
               self.targetSet.add( (self.scriptPath2Module( path )) );
            }

            let postProcessMap:Map<int,ConverterFunc> = {};
            foreach path, index in self.option.batchList {
               let mut updateInfo =
                  createUpdateInfo( path, (path.gsub( ".lns$", ".d" )) );
               print( "%s: start..." ( updateInfo.$scriptPath));
               if! process( false, updateInfo ) {
                  if self.option.$validPostBuild {
                     postProcessMap[ index ] = _exp;
                  } else {
                     _exp();
                  }
               }
            }
            forsort postProcess, index in postProcessMap {
               let prev = os.clock();
               let path = self.option.batchList[ index ];
               print( "%s: waiting..." (path) );
               postProcess();
               print( "%s: done %g msec" ( path, (os.clock() - prev) * 1000));
            }
         } else {
            if! let postProcess = process(
               true, createUpdateInfo( self.option.scriptPath, nil ) )
            {
               postProcess();
            }
         }

         when! astCallback {
            foreach mod in self.moduleMgr.getModList() {
               if! self.moduleMgr.getAst( mod ) {
                  astCallback( _exp );
               }
            }
         }
      }, self.option.scriptPath .. ".profi" );
}

pub fn build( option:Option.Option, astCallback:AstCallback ) mut {
   let mut front;
   __asyncLock {
      front = new Front( option ## );
   }
   front.build( .CreateAst, astCallback );
}

pub fn Front.exec() mut
{
   Log._log( .Trace, `{ return self.option.mode.$_txt; } );

   switch self.option.mode {
      case .Token {
         self.dumpTokenize( self.option.scriptPath );
      }
      case .Ast {
         self.dumpAst( self.option.scriptPath );
      }
      case .Format {
         self.format( self.option.scriptPath );
      }
      case .Diag {
         self.checkDiag( self.option.scriptPath );
      }
      case .Complete {
         self.complete( self.option.scriptPath );
      }
      case .Inquire {
         self.inquire( self.option.scriptPath );
      }
      case .Glue {
         self.createGlue( self.option.scriptPath );
      }
      case .Lua, .LuaMeta {
         let convMode:convLua.ConvMode;
         if self.option.mode == .Lua {
            convMode = .Convert;
         } else {
            convMode = .ConvMeta;
         }
         self.convertToLua(
            self.option.scriptPath, convMode, io.stdout, io.stdout );     
      }
      case .Save, .SaveMeta {
         self.build( .Save, nil );
      }
      case .BuildAst {
         self.build(
            .CreateAst,
            fn ( ast: &AstInfo.ASTInfo ) {
               print( ast.$streamName );
            });
      }
      case .Shebang {
         if! let modObj =
            self.loadModule( (self.scriptPath2Module( self.option.scriptPath )) )
         {
            __unsafe {
               let code = Depend.runMain( modObj.__main, self.option.shebangArgList );
               os.exit( code );
            }
         }
      }
      case .Exec {
         let modObj = self.loadModule(
            (self.scriptPath2Module( self.option.scriptPath )) );

         if self.option.testing {
            let code = ```
local Testing = require( "lune.base.Testing" )
return function( path )
  Testing.run( path );
  Testing.outputAllResult( io.stdout );
end
```;
            __unsafe {
               let loaded, mess = _load( code ## );
               when! loaded {
                  if! let mod = loaded(##) {
                     (mod@@form)( (self.scriptPath2Module( self.option.scriptPath )) );
                  }
               } else {
                  print( mess );
               }
            }
         }
      }
      case .BootC {
         self.outputBootC( self.option.scriptPath );
      }
      case .Builtin {
         self.outputBuiltin( self.option.scriptPath );
      }
      case .MkMain {
         let mod = self.scriptPath2Module( self.option.scriptPath );
         if! let mess = convGo.outputGoMain(
            self.getGoAppName(), mod, self.option.testing,
            self.option.outputPath, self.option.$runtimeOpt )
         {
            Util.errorLog( mess );
         }
      }
      case .Indexer {
         self.build(
            .CreateAst,
            fn ( ast: &AstInfo.ASTInfo ) {
               let mut indexer = new NodeIndexer.Indexer( ast.$exportInfo.$processInfo );
               indexer.start(
                  ast.$node,
                  (@
                    Nodes.NodeKind.$Switch,
                    Nodes.NodeKind.$Match,
                    Nodes.NodeKind.$For,
                    Nodes.NodeKind.$Apply
                  ) );
               indexer.dump();
            });
      }
      default {
         print( "illegal mode" );
      }
   }
}

pub fn exec( args: &List<str> ) __noasync {
   let version =
      unwrap tonumber( Depend.getLuaVersion().gsub( "^[^%d]+", "" ), nil ) default 0.0;

   if version < 5.1 {
      io.stderr.write(
         string.format( "LuneScript doesn't support this lua version(%s). %s\n",
        	        version, "please use the version >= 5.1.") );
      os.exit( 1 );
   }

   let mut option = Option.analyze( args );
   let mut front = new Front( option ## );

   front.exec();
}

// frontInterface.setFront( self ); で front を確定させるため
// go の runtime_go/depend.go から、初期化処理として呼ぶ。
pub fn setFront( bindModuleList:&List<str> ) __noasync {
   let mut option = Option.createDefaultOption( ["dummy.lns"], nil );
   new Front( option, bindModuleList );
}

pub fn __main( argList:&List<str> ) __noasync : int {
   let list:List<str> = [];
   foreach arg, index in argList {
      if index > 1 {
         list.insert( arg );
      }
   }

   exec( list );

   return 0;
}

__test case1(ctrl) {
   let luaCode = convertLnsCode2LuaCode( convLua.getTestLnsCode(), "test.lns", nil );
   convLua.runTestCode( ctrl, luaCode );

   exec( [ "lune/base/Ver.lns", "lua" ] );
}

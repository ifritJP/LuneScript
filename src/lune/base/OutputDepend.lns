/*
MIT License

Copyright (c) 2018 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

_lune_control default_async_all;


import lune.base.Nodes;
import lune.base.Util;
import lune.base.Ast;
import lune.base.AstInfo;

pub class DependInfo {
   let targetModule:str;
   let mut importModuleList:List<str>;
   let mut subModList:List<str>;

   pub fn __init( targetModule:str ) {
      self.targetModule = Ast.TypeInfo.getModulePath( targetModule );
      self.importModuleList = [];
      self.subModList = [];
   }

   pub fn addImpotModule( mod:str ) mut {
      self.importModuleList.insert( mod );
   }
   pub fn addSubMod( path:str ) mut {
      self.subModList.insert( path );
   }


   pub fn output( stream:oStream ) {
      stream.write( "%s.meta: \\\n" ( (self.targetModule.gsub( "%.", "/" ) )) );
      stream.write( "  %s.lns \\\n" ( (self.targetModule.gsub( "%.", "/" ) )) );
      foreach mod in self.importModuleList {
         stream.write( "  %s.meta \\\n" ((mod.gsub("%.", "/"))) );
      }
      foreach path in self.subModList {
         stream.write( "  %s.lns \\\n" ((path.gsub("%.", "/"))) );
      }
   }

}

class convFilter extend Nodes.Filter<int> {

   let mut stream:oStream;

   pub fn __init( stream:oStream )
   {
      super(false, nil,nil);
      self.stream = stream;
   }
}

pub override fn convFilter.processRoot(
   node: &Nodes.RootNode, dummy: int ) mut
{
   let moduleFull = node.$moduleTypeInfo.getFullName(
      self.$typeNameCtrl, Ast.DummyModuleInfoManager.$instance ## );
   let mut dependInfo = new DependInfo( moduleFull );

   foreach impNode in node.$nodeManager.getImportNodeList() {
      dependInfo.addImpotModule( impNode.$info.$modulePath );
   }
   foreach subfileNode in node.$nodeManager.getSubfileNodeList() {
      if! let usePath = subfileNode.$usePath {
         dependInfo.addSubMod( usePath );
      }
   }

   dependInfo.output( self.stream );
};

pub fn createFilter( stream: oStream ) : Nodes.Filter<int>
{
   return new convFilter( stream );
}

__test {
   import lune.base.convLua;
   import lune.base.frontInterface;
   import lune.base.LuaVer;
   import lune.base.Option;


   class Front extend (frontInterface.frontInterface) {
      //_lune_control default_noasync_this_class;
      
      pub fn loadModule( mod:str ) mut: Luaval<&stem>!, &frontInterface.ModuleMeta {
         Util.err( "not implements" ); // バグ
      }
      pub fn loadFromLnsTxt(
         importModuleInfo:frontInterface.ImportModuleInfo,
         baseDir:str!, name:str, txt:str ) mut : Luaval<&stem>
      {
         Util.err( "not implements" ); // バグ
      }
      pub fn getLuaModulePath( mod:str, baseDir:str! ) mut : str, str!, str {
         return mod, nil, mod;
      }
      pub fn searchModule( mod:str, baseDir:str!, addSearchPath:str! ) mut : str! {
         Util.err( "not implements" ); // バグ
      }
      pub fn error( message:str ) {
         Util.err( message ); // バグ
      }

      pub fn loadMeta(
         importModuleInfo:frontInterface.ImportModuleInfo, mod:str,
         orgMod:str, baseDir:str!, loader:frontInterface.ModuleLoader )
         mut: &frontInterface.ModuleMeta!
      {
         let mut metaPath = mod.gsub( "%.", "/" ) .. ".meta";
         when! baseDir {
            metaPath = Util.pathJoin( baseDir, metaPath );
         }
         Util.println( metaPath );
         let mess;
         let mut moduleMeta:&frontInterface.ModuleMeta! = nil;
         __luaLock {
            let loaded;
            loaded, mess = loadfile( metaPath );
            when! loaded {
               if! let meta = loaded(##) {
                  moduleMeta = new frontInterface.ModuleMeta(
                     (metaPath.gsub( "%.meta$", ".lns" )), .MetaRaw( meta ) );
               }
            }
         }
         when! moduleMeta {
            return moduleMeta;
         }
         Util.println( mess );
         return nil;
      }
   }
   

   fn Ast2Depend( ast:&AstInfo.ASTInfo ) __noasync : str
   {
      let mut stream = new Util.memStream();

      let mut conv = createFilter( stream );

      ast.$node.processFilter( conv, 0 );

      return stream.$txt;
   }
   
}

__test case1(ctrl) {
   LuaVer.setCurVer( 53 );

   let mut front = new Front();
   frontInterface.setFront( front );
   
   
   let code = ```
import lune.base.Ver;
import lune.base.Code;
import lune.base.LuaMod;

print( Ver.version );
print( Code.ID.nothing_define_abbr );
```;
   let scriptPath = "test.lns";
   let option = Option.createDefaultOption( [scriptPath], nil );
   let ast = convLua.createAst( scriptPath, code, option );
   let depend = Ast2Depend( ast );
   print( depend );
}


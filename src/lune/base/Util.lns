/*
MIT License

Copyright (c) 2018 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

import lune.base.Depend;

pri let mut debugFlag = true;
pub fn setDebugFlag( flag:bool ) {
   debugFlag = flag;
}
pri let mut errorCode = 1;
pub fn setErrorCode( code:int ) {
   errorCode = code;
}

pub fn errorLog( message: str ) {
   io.stderr.write( message .. "\n" );
}

pub fn err( message:str ): __ {
   if debugFlag {
      error( message );
   }
   errorLog( message );
   os.exit( errorCode );
}

/**
txt 文字列から、 pattern で構成される文字列を抜き出してリストを返す。
*/
pub fn splitStr( txt:str, pattern:str ) : &List<str> {
   let mut list:List<str> = [];
   apply token of string.gmatch( txt, pattern ) {
      list.insert( token );
   }
   return list;
}


pub class OrderedSet<T> {
   pri let mut set:Set<&T>;
   pri let mut list:List<&T> {pub&};

   pub fn __init() {
      self.set = (@);
      self.list = [];
   }

   pub fn add( val:&T ) mut : bool {
      if not self.set.has( val ) {
         self.set.add( val );
         self.list.insert( val );
         return true;
      }
      return false;
   }

   pub fn clone(): OrderedSet<T> {
      let mut obj = new OrderedSet<T>();
      foreach val in self.list {
         obj.set.add( val );
         obj.list.insert( val );
      }
      return obj;
   }

   pub fn has( val:&T ) : bool {
      return self.set.has( val );
   }

   pub fn removeLast() mut {
      if #self.list == 0 {
         err( "empty" );
      }
      self.set.del( self.list[ #self.list ] );
      self.list.remove(##);
   }
}

pub class memStream extend (oStream) {
   let mut txt: str { pub };
   pub fn __init() {
      self.txt = "";
   }
   pub fn write( val: str ) mut : stem!, str! {
      self.txt = self.txt .. val;
      return self, nil;
   }
   pub fn close() mut {
   }
   pub fn flush() mut {
   }
}

pub interface SourceStream {
   pub abstract fn write( txt: str ) mut;
   pub abstract fn writeln( txt: str ) mut;
   pub abstract fn pushIndent( newIndent:int! ) mut;
   pub abstract fn popIndent() mut;
   pub abstract fn switchToHeader() mut;
   pub abstract fn returnToSource() mut;
}

pub class SimpleSourceOStream extend (SourceStream) {
   let mut nowStream:oStream;
   let mut srcStream:oStream;
   let mut headStream:oStream;
   let mut needIndent:bool;
   let stepIndent:int;
   let mut curLineNo:int;
   pri let mut indentQueue: List<int>;

   pub fn __init( stream:oStream, headStream:oStream!, stepIndent:int ) {
      self.srcStream = stream;
      self.nowStream = stream;
      self.headStream = unwrap headStream default stream;
      self.needIndent = true;
      self.curLineNo = 0;
      self.stepIndent = stepIndent;
      self.indentQueue = [ 0 ];
   }

   fn get_indent(): int {
      if #self.indentQueue > 0 {
         return self.indentQueue[ #self.indentQueue ];
      }
      return 0;
   }
   
   fn writeRaw( txt: str ) mut {
      let mut stream = self.nowStream;

      if self.needIndent {
         stream.write( string.rep( " ", self.$indent ) );
         self.needIndent = false;
      }

      apply _ of string.gmatch( txt, "\n" ) {
         self.curLineNo = self.curLineNo + 1;
      }
      stream.write( txt );
   }

   pub fn write( mut txt: str ) mut {
      while true {
         if! let index = string.find( txt, "\n"## ) {
            self.writeRaw( txt.sub( 1, index ) );
            txt = txt.sub( index + 1 ##);
         }
         else {
            break;
         }
      }
      if #txt > 0 {
         self.writeRaw( txt );
      }
   }

   pub fn writeln( txt: str ) mut {
      self.write( txt );
      self.write( "\n" );
      self.needIndent = true;
   }

   pub fn pushIndent( newIndent:int! ) mut {
      let indent = unwrap newIndent default self.$indent + self.stepIndent;
      self.indentQueue.insert( indent );
   }

   pub fn popIndent() mut {
      if #self.indentQueue == 0 {
         err( "self.indentQueue == 0" );
      }
      self.indentQueue.remove(##);
   }

   pub fn switchToHeader() mut {
      self.nowStream = self.headStream;
   }
   pub fn returnToSource() mut {
      self.nowStream = self.srcStream;
   }
}

pub fn log( message:str ) {
   if debugFlag {
      errorLog( message );
   }
}

pub fn printStackTrace() {
   errorLog( Depend.getStackTrace() );
}

/**
path で指定した lns コードに対応する Lua コードが存在する場合、
その Lua コードのパスを返す。
*/
pub fn getReadyCode( lnsPath:str, luaPath:str ): bool {
   let! luaTime, lnsTime =
      Depend.getFileLastModifiedTime( luaPath ),
      Depend.getFileLastModifiedTime( lnsPath )
   {
      // errorLog( "getReadyCode: false, %s %s" (luaPath, lnsPath ) );
      return false;
   };
   // errorLog( "getReadyCode: %s %s %s (%f, %f)"
   //           (luaTime >= lnsTime, luaPath, lnsPath, luaTime, lnsTime ) );
   return luaTime >= lnsTime;
}

pub fn scriptPath2Module( path:str ):str {
   if path.find( "^/" ## ) {
      err( "script must be relative-path -- " .. path );
   }
   let mod = string.gsub( path, "/", "." );
   return ( string.gsub( mod, "%.lns$", "" ) );
}


__test case1(ctrl) {
   {
      // splitStr のテスト
      let splitList = splitStr( "123 456 789", "[0-9]+" );
      __t._eq( ctrl, #splitList, 3 );
      __t._eq( ctrl, splitList[ 1 ], "123" );
      __t._eq( ctrl, splitList[ 2 ], "456" );
      __t._eq( ctrl, splitList[ 3 ], "789" );
   }

   {
      // OrderedSet<T> のテスト
      let mut orderdSet = new OrderedSet<str>();
      orderdSet.add( "abc" );
      orderdSet.add( "def" ); 
      orderdSet.add( "ghi" );
      orderdSet.add( "jkl" );
      orderdSet.add( "nmo" );
      orderdSet.add( "pqr" );
      orderdSet.add( "stu" );
      orderdSet.add( "vwx" );
      orderdSet.add( "yz" );

      // 同じものを追加して、登録されないことを確認する。
      orderdSet.add( "jkl" ); 

      // 順番通りにリストされることを確認する
      let mut concat = "";
      foreach val in orderdSet.$list {
         concat = concat .. val;
      }
      __t._eq( ctrl, "abcdefghijklnmopqrstuvwxyz", concat );
   }

   {
      // memStream のテスト
      let mut stream = new memStream();
      stream.write( "12345" );
      stream.write( "67890" );
      __t._eq( ctrl, stream.$txt, "1234567890" );
   }

   {
      // SimpleSourceOStream のテスト
      let mut stream = new memStream();
      let mut sourceStream = new SimpleSourceOStream( stream, nil, 3 );

      sourceStream.writeln( "{" );
      sourceStream.pushIndent( ## );
      sourceStream.writeln( "let val = 1;" );
      sourceStream.writeln( "{" );
      sourceStream.pushIndent( ## );
      sourceStream.writeln( "let val2 = 2;" );
      sourceStream.popIndent();
      sourceStream.writeln( "}" );
      sourceStream.popIndent();
      sourceStream.write( "}" );
      sourceStream.popIndent();

      __t._eq( ctrl, stream.$txt,
```{
   let val = 1;
   {
      let val2 = 2;
   }
}``` );
   }
}

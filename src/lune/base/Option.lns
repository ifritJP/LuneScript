/*
MIT License

Copyright (c) 2018 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

import lune.base.Parser;
import lune.base.Util;

pub let version = "00.01";

pub class Option {
   pub let mode:str;
   pub let analyzeModule:str!;
   pub let analyzePos: Parser.Position!;
   pub let outputDir:str!;
   pub let scriptPath: str;
   pub let validProf:bool;
   pub fn __init() {
      self.validProf = false;
      self.mode = "";
      self.scriptPath = "";
   }
}

pub fn analyze( argList: str[@] ) : Option {
   if #argList < 2 {
      print( ```
usage: [-prof] src.lns mode [mode-option]
  - src.lns ast
  - src.lns comp [-i] module line column
  - src.lns [lua|LUA] 
  - src.lns [save|SAVE] output-dir
  - src.lns exe
``` );
      os.exit( 1 );
   }

   let option = new Option();
   let useStdInFlag = false;
   let lineNo:int! = nil;
   let column:int! = nil;

   let index = 1;
   while #argList >= index {
      let arg = argList[ index ];

      if arg.find( "^-" ) {
         switch ( arg ) {
            case "-i" {     
               useStdInFlag = true;
            }
            case "-prof" {
               option.validProf = true;
            }
            case "--nodebug" {
               Util.setDebugFlag( false );
            }
            default {
               Util.log( "unknown option -- %s" (arg) );
               os.exit( 1 );
            }
         }
      }
      else {
         if option.scriptPath == "" {
            option.scriptPath = arg;
         }
         elseif option.mode == "" {
            option.mode = arg;
         }
         else {
            switch( option.mode ) {
               case "comp" {
                  if not option.analyzeModule {
                     option.analyzeModule = arg;
                  }
                  elseif not lineNo {
                     lineNo = tonumber( arg )@@int;
                  }
                  elseif not column {
                     column = tonumber( arg )@@int;
                     option.analyzePos = new Parser.Position(
                        unwrap lineNo, unwrap column );
                  }
               }
               case "save", "SAVE" {
                  option.outputDir = arg;
               }
               default {
                  Util.err( "unknown option for this mode (%s) -- %s"
                              (option.mode, arg ) );
               }
            }
         }
      }

      index = index + 1;
   }

   if useStdInFlag and option.analyzeModule {
      Parser.StreamParser.setStdinStream( unwrap option.analyzeModule );
   }

   return option;
}

/*
MIT License

Copyright (c) 2018 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

import lune.base.Parser;
import lune.base.Util;

pub let version = "00.01";

pub enum ModeKind {
   Unknown = "",
   Token = "token",
   Ast = "ast",
   Diag = "diag",
   Complete = "comp",
   Lua = "lua",
   LuaMeta = "LUA",
   Save = "save",
   SaveMeta = "SAVE",
   Exec = "exe",
   Glue = "glue",
}

pub class Option {
   pub let mut mode:ModeKind;
   pub let analyzeModule:str!;
   pub let analyzePos: Parser.Position!;
   pub let outputDir:str!;
   pub let mut scriptPath: str;
   pub let mut validProf:bool;
   pub fn __init() {
      self.validProf = false;
      self.mode = .Unknown;
      self.scriptPath = "";
   }
}

pub fn analyze( argList: str[] ) : Option {

   if #argList < 2 {
      print( ```
usage: [-prof] src.lns mode [mode-option]
  - src.lns ast
  - src.lns comp [-i] module line column
  - src.lns [lua|LUA] 
  - src.lns [save|SAVE] output-dir
  - src.lns exe
``` );
      os.exit( 1 );
   }

   let mut option = new Option();
   let mut useStdInFlag = false;
   let mut lineNo:int! = nil;
   let mut column:int! = nil;

   let mut index = 1;
   while #argList >= index {
      let arg = argList[ index ];

      if arg.find( "^-" ) {
         switch ( arg ) {
            case "-i" {     
               useStdInFlag = true;
            }
            case "-prof" {
               option.validProf = true;
            }
            case "--nodebug" {
               Util.setDebugFlag( false );
            }
            default {
               Util.log( "unknown option -- %s" (arg) );
               os.exit( 1 );
            }
         }
      }
      else {
         if option.scriptPath == "" {
            option.scriptPath = arg;
         }
         elseif option.mode == "" {
            if! let mode = ModeKind._from( arg ) {
               option.mode = mode;
            }
            else {
               Util.err( "unknown mode -- %s" ( arg ) );
               os.exit( 1 );
            }
         }
         else {
            switch( option.mode ) {
               case .Complete {
                  if not option.analyzeModule {
                     option.analyzeModule = arg;
                  }
                  elseif not lineNo {
                     lineNo = tonumber( arg )@@int;
                  }
                  elseif not column {
                     column = tonumber( arg )@@int;
                     option.analyzePos = new Parser.Position(
                        unwrap lineNo, unwrap column );
                  }
               }
               case .Save, .SaveMeta, .Glue {
                  option.outputDir = arg;
               }
               default {
               }
            }
         }
      }

      index = index + 1;
   }

   if useStdInFlag and option.analyzeModule {
      Parser.StreamParser.setStdinStream( unwrap option.analyzeModule );
   }

   return option;
}

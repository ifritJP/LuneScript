/*
MIT License

Copyright (c) 2019 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

_lune_control default_async_all;

import lune.base.Async;

pub enum Level {
   Fatal,
   Err,
   Warn,
   Log,
   Info,
   Debug,
   Trace,
}

let name2levelMap:&Map<str,Level>;

{
   let work:Map<str,Level> = {};
   work[ "fatal" ] = .Fatal;
   work[ "error" ] = .Err;
   work[ "warn" ] = .Warn;
   work[ "log" ] = .Log;
   work[ "info" ] = .Info;
   work[ "debug" ] = .Debug;
   work[ "trace" ] = .Trace;
   name2levelMap = work;
}

pub fn str2level( txt:str ) : Level! {
   return name2levelMap[ txt ];
}

pub form CreateMessage() __trans : str;
class Control {
   let level:Level;
   local fn log( level:Level, funcName:str, lineNo:int, callback: CreateMessage ) {
      let mut logStream:oStream = io.stderr;
      if level <= self.level {
         let nowClock = os.clock();

         logStream.write(
            "%6d:%s:%s:%d:" (( nowClock * 1000 )@@int,
                              level.$_txt, funcName, lineNo) );
         logStream.write( callback() );
         logStream.write( "\n" );
      }
   }
   local fn direct( level:Level, funcName:str, lineNo:int, mess:str ) {
      self.log( level, funcName, lineNo,
                fn () : str {
                   return mess;
                } );
   }
}

let mut control:&Control = new Control( Level.Err );

pub fn setLevel( level:Level ) __noasync {
   control = new Control( level );
}


pub fn log( level:Level, funcName:str, lineNo:int, callback: CreateMessage ) {
   control.log( level, funcName, lineNo, callback );
}

pub fn direct( level:Level, funcName:str, lineNo:int, mess:str) {
   control.direct( level, funcName, lineNo, mess );
}

pub macro _log( level:Level, mess : stat ) {
   Log.log( ,,level, __func__, __line__, fn () __trans :str { ,,mess } );
}

pub macro _log2( level:Level, mess : __exp ) {
   Log.log( ,,level, __func__, __line__, fn () __trans :str { return ,,mess; } );
}

pub macro _log3( level:Level, mess : __exp ) {
   Log.log( ,,level, __func__, __line__, fn () __noasync :str { return ,,mess; } );
}

/*
MIT License

Copyright (c) 2018 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

import lune.base.Ast;
import lune.base.Parser;

pub class Opt {
   let prefix: str;
   let depth: int;
   let mut next: Opt!;

   pub fn __init( prefix:str, depth:int ) {
      self.prefix = prefix;
      self.depth = depth;
      self.next = nil;
   }
   pub fn get(): str, int {
      return self.prefix, self.depth;
   }
   pub fn nextOpt() mut : Opt {
      if! self.next {
         return _exp;
      }
      let mut opt = new Opt( self.prefix .. "  ", self.depth + 1 );
      self.next = opt;
      return opt;
   }
}

class dumpFilter extend Ast.Filter<Opt> {
}

pub fn createFilter(): Ast.Filter<Opt> {
   return new dumpFilter();
}

let mut localFilter = new dumpFilter();

fn dump( prefix:str, depth:int, node: &Ast.Node, txt: str ) {
   let mut typeStr = "";
   let expType = node.get_expType();
   if expType.equals( Ast.builtinTypeNone ##) {
      typeStr = "(%d:%s:%s)" ( expType.get_typeId(),
                               expType.getTxt(##),
                               expType.get_kind() );
   }
   print( "%s: %s %s %s"
	  (prefix, Ast.getNodeKindName( node.get_kind() ), txt, typeStr ) );
}

fn filter( node: &Ast.Node, filter: &dumpFilter, opt:Opt ) {
   node.processFilter( localFilter, opt );
}

fn getTxt( token: Parser.Token ): str {
   return token.txt;
}

pub override fn dumpFilter.processNone( 
   node: &Ast.NoneNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
};
pub override fn dumpFilter.processImport( 
   node: &Ast.ImportNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_modulePath() );
};
pub override fn dumpFilter.processRoot( 
   node: &Ast.RootNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   foreach child, index in node.get_children() {
      filter( child, self, opt.nextOpt() );
   }	
};
pub override fn dumpFilter.processSubfile( 
   node: &Ast.SubfileNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
}
pub override fn dumpFilter.processBlock( 
   node: &Ast.BlockNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   foreach statement, index in node.get_stmtList() {
      filter( statement, self, opt.nextOpt() );
   }	
};  
pub override fn dumpFilter.processStmtExp( 
   node: &Ast.StmtExpNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   filter( node.get_exp(), self, opt.nextOpt() );
};

pub override fn dumpFilter.processDeclEnum(
   node: &Ast.DeclEnumNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.$name.txt );
   let enumTypeInfo = node.$expType@@Ast.EnumTypeInfo;
   foreach name in node.$valueNameList {
      let valInfo = unwrap enumTypeInfo.getEnumValInfo( name.txt );
      print( "%s  %s: %s" ( prefix, name.txt, valInfo.$val ) );
   }
}

pub override fn dumpFilter.processDeclAlge(
   node: &Ast.DeclAlgeNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   let algeTypeInfo = node.$algeType;
   dump( prefix, depth, node, algeTypeInfo.$rawTxt );
   forsort valInfo in algeTypeInfo.$valInfoMap {
      print( "%s  %s: %s" ( prefix, algeTypeInfo.$rawTxt, valInfo.$name ) );
   }
}

pub override fn dumpFilter.processNewAlgeVal(
   node: &Ast.NewAlgeValNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.$name.txt );
   foreach exp in node.$paramList {
      filter( exp, self, opt.nextOpt() );
   }
}


pub override fn dumpFilter.processDeclClass( 
   node: &Ast.DeclClassNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_name().txt );
   foreach field, index in node.get_fieldList() {
      filter( field, self, opt.nextOpt() );
   }	
};  
pub override fn dumpFilter.processDeclMember( 
   node: &Ast.DeclMemberNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_name().txt );
   filter( node.get_refType(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processExpMacroExp( 
   node: &Ast.ExpMacroExpNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   let stmtList = node.get_stmtList();
   foreach stmt in stmtList {
      filter( stmt, self,  opt.nextOpt() );
   }
};
pub override fn dumpFilter.processDeclMacro( 
   node: &Ast.DeclMacroNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_expType().getTxt(##) );
};
pub override fn dumpFilter.processExpMacroStat( 
   node: &Ast.ExpMacroStatNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_expType().getTxt(##) );
   foreach node in node.get_expStrList() {
      filter( node, self, opt.nextOpt() );
   }
};


pub override fn dumpFilter.processUnwrapSet( 
   node: &Ast.UnwrapSetNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   filter( node.$dstExpList, self, opt.nextOpt() );
   filter( node.$srcExpList, self, opt.nextOpt() );

   if node.$unwrapBlock {
      filter( unwrap node.$unwrapBlock, self, opt.nextOpt() );
   }
}

pub override fn dumpFilter.processIfUnwrap( 
   node: &Ast.IfUnwrapNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   foreach expNode, index in node.$expNodeList {
      filter( expNode, self, opt.nextOpt() );
   }
   filter( node.$block, self, opt.nextOpt() );
   if node.$nilBlock {
      filter( unwrap node.$nilBlock, self, opt.nextOpt() );
   }
}

pub override fn dumpFilter.processWhen( 
   node: &Ast.WhenNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   foreach expNode, index in node.$expNodeList {
      filter( expNode, self, opt.nextOpt() );
   }
   filter( node.$block, self, opt.nextOpt() );
   if! node.$elseBlock {
      filter( _exp, self, opt.nextOpt() );
   }
}


pub override fn dumpFilter.processDeclVar( 
   node: &Ast.DeclVarNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   let mut varName = "";
   foreach var, index in node.get_varList() {
      if index > 1 {
	 varName = varName .. ",";
      }
      varName = "%s %s" (varName, var.get_name().txt );
   }
   if node.$unwrapBlock {
      varName = "!" .. varName;
   }
   varName = "%s %s" ( node.$mode, varName );
   
   dump( prefix, depth, node, varName );
   foreach var, index in node.get_varList() {
      if! var.$refType {
         filter( _exp, self, opt.nextOpt() );
      }
   }
   if! node.get_expList() {
      filter( _exp, self, opt.nextOpt() );
   }
   if! node.$unwrapBlock {
      filter( _exp, self, opt.nextOpt() );
   }
   if! node.$thenBlock {
      filter( _exp, self, opt.nextOpt() );
   }
   foreach var in node.$syncVarList {
      if! var.$refType {
         filter( _exp, self, opt.nextOpt() );
      }
   }
   if! node.$syncBlock {
      filter( _exp, self, opt.nextOpt() );
   }
};  
pub override fn dumpFilter.processDeclArg( 
   node: &Ast.DeclArgNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_name().txt );
   filter( node.get_argType(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processDeclArgDDD( 
   node: &Ast.DeclArgDDDNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "..." );
};  
pub override fn dumpFilter.processExpDDD( 
   node: &Ast.ExpDDDNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "..." );
};

fn dumpFilter.processDeclFuncInfo(
   node: &Ast.Node, declInfo: Ast.DeclFuncInfo, opt:Opt )
{
   let prefix, depth = opt.get();
   let mut name = "<anonymous>";
   if! declInfo.get_name() {
      name = _exp.txt;
   }
   if node.$expType.$mutable {
      name = name .. " mut";
   }
   dump( prefix, depth, node, name );
   let argList = declInfo.get_argList();
   foreach arg, index in argList {
      filter( arg, self, opt.nextOpt() );
   }
   if! declInfo.get_body() {
      filter( _exp, self, opt.nextOpt() );
   }
}
pub override fn dumpFilter.processDeclFunc( 
   node: &Ast.DeclFuncNode, opt:Opt ) mut
{
   self.processDeclFuncInfo( node, node.get_declInfo(), opt );
};	
pub override fn dumpFilter.processDeclMethod( 
   node: &Ast.DeclMethodNode, opt:Opt ) mut
{
   self.processDeclFuncInfo( node, node.get_declInfo(), opt );
};	
pub override fn dumpFilter.processDeclConstr( 
   node: &Ast.DeclConstrNode, opt:Opt ) mut
{
   self.processDeclFuncInfo( node, node.get_declInfo(), opt );
};

pub override fn dumpFilter.processDeclDestr( 
   node: &Ast.DeclDestrNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
};

pub override fn dumpFilter.processExpCallSuper( 
   node: &Ast.ExpCallSuperNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   let typeInfo:Ast.TypeInfo = node.get_superType();
   dump( prefix, depth, node, typeInfo.getTxt(##) );
};
pub override fn dumpFilter.processRefType( 
   node: &Ast.RefTypeNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node,
	 (node.get_refFlag() and "&" or "") ..
	 (node.get_mutFlag() and "mut " or "") );
   filter( node.get_name(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processIf( 
   node: &Ast.IfNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "");
   let stmtList = node.get_stmtList();
   foreach stmt, index in stmtList {
      if stmt.$exp.$kind ~= Ast.nodeKind.None {
	 filter( stmt.$exp, self, opt.nextOpt() );
      }   	
      filter( stmt.$block, self, opt.nextOpt() );
   }	
};  
pub override fn dumpFilter.processSwitch( 
   node: &Ast.SwitchNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "");
   filter( node.get_exp(), self, opt.nextOpt() );
   let caseList = node.$caseList;
   foreach caseInfo in caseList {
      filter( caseInfo.$expList, self, opt.nextOpt() );
      filter( caseInfo.$block, self, opt.nextOpt() );
   }
   if! node.get_default() {
      filter( _exp, self, opt.nextOpt() );
   }
};
pub override fn dumpFilter.processMatch( 
   node: &Ast.MatchNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "");
   filter( node.$val, self, opt.nextOpt() );
   let caseList = node.$caseList;
   foreach caseInfo in caseList {
      filter( caseInfo.$block, self,
              new Opt( prefix .. "  " .. caseInfo.$valInfo.$name, depth + 1 ) );
   }
   if! node.$defaultBlock {
      filter( _exp, self, opt.nextOpt() );
   }
};
pub override fn dumpFilter.processWhile( 
   node: &Ast.WhileNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "");
   filter( node.get_exp(), self, opt.nextOpt() );
   filter( node.get_block(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processRepeat( 
   node: &Ast.RepeatNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "");
   filter( node.get_block(), self, opt.nextOpt() );
   filter( node.get_exp(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processFor( 
   node: &Ast.ForNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_val().txt );
   filter( node.get_init(), self, opt.nextOpt() );
   filter( node.get_to(), self, opt.nextOpt() );
   if! node.get_delta() {
      filter( _exp, self, opt.nextOpt() );
   }	
   filter( node.get_block(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processApply( 
   node: &Ast.ApplyNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   let mut varNames = "";
   let varList = node.get_varList();
   foreach var, index in varList {
      varNames = varNames .. var.txt .. " ";
   }	
   dump( prefix, depth, node, varNames );
   filter( node.get_exp(), self, opt.nextOpt() );
   filter( node.get_block(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processForeach( 
   node: &Ast.ForeachNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   let mut index = "";
   if! node.$key {
      index = _exp.txt;
   }
   if! node.$val {
      dump( prefix, depth, node, _exp.txt .. " " .. index );
   }
   filter( node.get_exp(), self, opt.nextOpt() );
   filter( node.get_block(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processForsort( 
   node: &Ast.ForsortNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   let mut index = "";
   if! node.get_key() {
      index = _exp.txt;
   }
   dump( prefix, depth, node, node.get_val().txt .. " " .. index );
   filter( node.get_exp(), self, opt.nextOpt() );
   filter( node.get_block(), self, opt.nextOpt() );
};

pub override fn dumpFilter.processExpUnwrap(
   node: &Ast.ExpUnwrapNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   filter( node.$exp, self, opt.nextOpt() );
   if! node.$default {
      filter( _exp, self, opt.nextOpt() );
   }
}

pub override fn dumpFilter.processExpCall( 
   node: &Ast.ExpCallNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   filter( node.get_func(), self, opt.nextOpt() );
   if! node.get_argList() {
      filter( _exp, self, opt.nextOpt() );
   }	
};  
pub override fn dumpFilter.processExpList( 
   node: &Ast.ExpListNode, opt:Opt ) mut

{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   let expList = node.get_expList();
   foreach exp, index in expList {
      filter( exp, self, opt.nextOpt() );
   }	
};  
pub override fn dumpFilter.processExpOp1( 
   node: &Ast.ExpOp1Node, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_op().txt );
   filter( node.get_exp(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processExpCast( 
   node: &Ast.ExpCastNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   filter( node.get_exp(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processExpParen( 
   node: &Ast.ExpParenNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "()" );
   filter( node.get_exp(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processExpOp2( 
   node: &Ast.ExpOp2Node, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_op().txt );
   filter( node.get_exp1(), self, opt.nextOpt() );
   filter( node.get_exp2(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processExpNew( 
   node: &Ast.ExpNewNode, opt:Opt ) mut

{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   filter( node.get_symbol(), self, opt.nextOpt() );
   if! node.get_argList() {
      filter( _exp, self, opt.nextOpt() );
   }
};
pub override fn dumpFilter.processExpRef( 
   node: &Ast.ExpRefNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_token().txt );
};
pub override fn dumpFilter.processExpRefItem( 
   node: &Ast.ExpRefItemNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "seq[exp] " .. node.get_expType().getTxt(##) );
   filter( node.$val, self, opt.nextOpt() );
   if! node.$index {
      filter( _exp, self, opt.nextOpt() );
   }
};  
pub override fn dumpFilter.processRefField( 
   node: &Ast.RefFieldNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_field().txt );
   filter( node.get_prefix(), self, opt.nextOpt() );
};  
pub override fn dumpFilter.processExpOmitEnum( 
   node: &Ast.ExpOmitEnumNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node,
         "%s.%s" (node.$expType.getTxt(##), node.$valToken.txt ) );
};

pub override fn dumpFilter.processGetField( 
   node: &Ast.GetFieldNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "get_" .. node.get_field().txt );
   filter( node.get_prefix(), self, opt.nextOpt() );
};

pub override fn dumpFilter.processReturn( 
   node: &Ast.ReturnNode, opt:Opt ) mut

{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   if! node.get_expList() {
      filter( _exp, self, opt.nextOpt() );
   }
};

pub override fn dumpFilter.processProvide(
   node: &Ast.ProvideNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.$symbol.$name );
}

pub override fn dumpFilter.processAlias(
   node: &Ast.AliasNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "%s = %s" (node.$newName, node.$typeInfo.getTxt(##) ) );
}

pub override fn dumpFilter.processBoxing(
   node: &Ast.BoxingNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   filter( node.$src, self, opt.nextOpt() );
}

pub override fn dumpFilter.processUnboxing(
   node: &Ast.UnboxingNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
   filter( node.$src, self, opt.nextOpt() );
}

pub override fn dumpFilter.processLiteralList( 
   node: &Ast.LiteralListNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "[]" );
   if! node.get_expList() {
      filter( _exp, self, opt.nextOpt() );
   }
};  
pub override fn dumpFilter.processLiteralSet( 
   node: &Ast.LiteralSetNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "(@)" );
   if! node.get_expList() {
      filter( _exp, self, opt.nextOpt() );
   }
};  
pub override fn dumpFilter.processLiteralMap( 
   node: &Ast.LiteralMapNode, opt:Opt ) mut

{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "{}" );
   let pairList = node.get_pairList();
   foreach pair in pairList {
      filter( pair.get_key(), self, opt.nextOpt() );
      filter( pair.get_val(), self, opt.nextOpt() );
   }
};  
pub override fn dumpFilter.processLiteralArray( 
   node: &Ast.LiteralArrayNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "[@]" );
   if! node.get_expList() {
      filter( _exp, self, opt.nextOpt() );
   }
};  
pub override fn dumpFilter.processLiteralChar( 
   node: &Ast.LiteralCharNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node,
	 string.format( "%s(%s)", node.get_num(), node.get_token().txt ) );
};  
pub override fn dumpFilter.processLiteralInt( 
   node: &Ast.LiteralIntNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node,
	 string.format( "%s(%s)", node.get_num(), node.get_token().txt ) );
};  
pub override fn dumpFilter.processLiteralReal( 
   node: &Ast.LiteralRealNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node,
	 string.format( "%s(%s)", node.get_num(), node.get_token().txt ) );
};  
pub override fn dumpFilter.processLiteralString( 
   node: &Ast.LiteralStringNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_token().txt );
   foreach param in node.$argList {
      filter( param, self, opt.nextOpt() );
   }
};  
pub override fn dumpFilter.processLiteralBool( 
   node: &Ast.LiteralBoolNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node,
         node.get_token().txt == "true" and "true" or "false" );
};  
pub override fn dumpFilter.processLiteralNil( 
   node: &Ast.LiteralNilNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
};  
pub override fn dumpFilter.processBreak( 
   node: &Ast.BreakNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, "" );
};  
pub override fn dumpFilter.processLiteralSymbol( 
   node: &Ast.LiteralSymbolNode, opt:Opt ) mut
{
   let prefix, depth = opt.get();
   dump( prefix, depth, node, node.get_token().txt );
};  

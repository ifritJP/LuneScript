/*
MIT License

Copyright (c) 2018 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

import lune.base.Parser;
import lune.base.Util;
import lune.base.LuaMod;
import lune.base.Ver;
import lune.base.LuaVer;
import lune.base.Log;
import lune.base.Ast;

macro _defGetBuildCount() {
   {
      let mut count = 0;
      if! let mut fileObj = io.open( "build.counter" ## ) {
         if! let txt = fileObj.read( "*l" ) {
            count = (unwrap tonumber( txt ## ) default 0)@@int;
         }
      }
      if count == 0 {
         error( "error -- build.counter" );
      }
   }
   fn getBuildCount(): int {
      return ,,count;
   }
}

_defGetBuildCount();

pub enum ModeKind {
   Unknown = "",
   Token = "token",
   Ast = "ast",
   Diag = "diag",
   Complete = "comp",
   Lua = "lua",
   LuaMeta = "LUA",
   Save = "save",
   SaveMeta = "SAVE",
   Exec = "exe",
   Glue = "glue",
   BootC = "bootC",
   Format = "format",
}

/** meta ファイルの更新必要性チェックモード */
pub enum CheckingUptodateMode {
   /** uptodate チェックせずに必ずビルドしなおす */
   Force = "force",
   /** uptodate チェックし、 uptodate だった場合なにもしない */
   Normal = "none",
   /** uptodate チェックし、 uptodate だった場合 touch だけ行なう */
   Touch = "touch",
}

pub class TransCtrlInfo {
   pub let mut checkingDefineAbbr:bool;
   pub let mut stopByWarning:bool;
   pub let mut uptodateMode:CheckingUptodateMode;
}

pub static fn TransCtrlInfo.create_normal(): TransCtrlInfo {
   return new TransCtrlInfo( true, false, .Touch );
}

pub class Option {
   pub let mut mode:ModeKind;
   /** 解析対象のモジール名。 comp モードの時に使用。 */
   pub let analyzeModule:str!;
   /** 解析位置。comp モードの時に使用。 */
   pub let analyzePos: Parser.Position!;
   /** 出力ディレクトリ。 save, SAVE モードの時に使用*/
   pub let outputDir:str!;
   /** 処理対象のスクリプトファイルパス */
   pub let mut scriptPath: str;
   /** プロファイルする場合 true */
   pub let mut validProf:bool;
   /** true の場合 require( 'lune.base._lune' ) する */
   pub let mut useLuneModule:str!;
   /** 依存ファイル情報の出力先ストリーム */
   //pub let mut dependsStream:oStream!;
   /** 依存ファイル情報の出力先ファイルパス */
   pub let mut dependsPath:str!;
   /** ロード時に meta 等を更新する  */
   pub let mut updateOnLoad:bool;
   /** セーブ時に byte コンパイルする */
   pub let mut byteCompile:bool;
   /** byte コンパイルする際にデバッグ情報を削除する */
   pub let mut stripDebugInfo:bool;
   /** 変換先の Lua バージョン*/
   pub let mut targetLuaVer:&LuaVer.LuaVerInfo;
   pub let mut bootPath:str!;

   pub let mut transCtrlInfo:TransCtrlInfo;

   /** true の場合 C への変換を行なう */
   pub let mut convertC:bool;

   /** テストする時 true */
   pub let mut testing:bool;

   pub fn __init() {
      self.testing = false;
      self.convertC = false;
      self.validProf = false;
      self.mode = .Unknown;
      self.scriptPath = "";
      self.useLuneModule = nil;
      self.updateOnLoad = false;
      self.byteCompile = false;
      self.stripDebugInfo = false;
      self.targetLuaVer = LuaVer.curVer;
      self.transCtrlInfo = TransCtrlInfo.create_normal();
      self.bootPath = nil;
   }

   pub fn openDepend(): mut oStream! {
      if! let path = self.dependsPath {
         return io.open( path, "w" );
      }
      return nil;
   }
}

pub fn outputLuneMod( path:str! ):str! {
   let mut lune_path = "_lune.lua";
   when! path {
      if path ~= "" {
         lune_path = path;
      }
   }
   let! mut fileObj = io.open( lune_path, "w" ) {
      return "failed to open -- %s" ( lune_path );
   };

   fileObj.write( ```
--[[
MIT License

Copyright (c) 2018,2019 ifritJP

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
]]
```);

   foreach kind in LuaMod.CodeKind.$_allList {
      fileObj.write( LuaMod.getCode( kind ) );
   }

   fileObj.close();
   return nil;
}

pub fn analyze( argList: str[] ) : Option {

   fn printUsage(code:int) {
      print( ```
usage:
  <type1> [-prof] [-r] src.lns mode [mode-option]
  <type2> -mklunemod path
  <type3> --version

* type1
  - src.lns [common_op] ast
  - src.lns [common_op] comp [-i] module line column
  - src.lns [common_op] [-ol ver] [-ob<0|1>] [-dmr] <lua|LUA>
  - src.lns [common_op] [-ol ver] [-ob<0|1>] [-dmr] [--depends dependfile] <save|SAVE> output-dir
  - src.lns [common_op] exe

  -r: use 'require( "lune.base._lune" )'
  -ol: output lua version. ver = 51 or 52 or 53.
  -ob: output bytecompiled-code.
      -ob0 is without debug information.
      -ob1 is with debug information.
  -oc: output path of the source code transcompiled to c-lang .
  --depends: output dependfile

  common_op:
    -u: update meta and lua on load.
    -Werror: error by warrning.
    --log <mode>: set log level.
         mode: fatal, error, warn, info, debug, trace
    --disable-checking-define-abbr: disable checking for ##.
    --uptodate <mode>: checking uptodate mode.
            mode: skip check.
            none: skip process when file is uptodate.
            touch: touch meta file when file is uptodate.  (default)

* type2
  dir: output directory.
``` );
      os.exit( code );
   }

   let mut option = new Option();
   let mut useStdInFlag = false;
   let mut lineNo:int! = nil;
   let mut column:int! = nil;

  
   let mut index = 1;

   fn getNextOp():str! {
      if #argList <= index {
         return nil;
      }
      index = index + 1;
      return argList[ index ];
   }
   
   while #argList >= index {
      let arg = argList[ index ];

      if arg.find( "^-" ## ) {
         switch ( arg ) {
            case "-i" {
               useStdInFlag = true;
            }
            case "-prof" {
               option.validProf = true;
            }
            case "--nodebug" {
               Util.setDebugFlag( false );
            }
            case "--version" {
               print( "LuneScript: version %s (%d:%s)"
                  (Ver.version, getBuildCount(), _VERSION) );
               os.exit( 0 );
            }
            case "--builtin" {
               forsort typeInfo, typeId in Ast.builtInTypeIdSet {
                  print( typeId, typeInfo.getTxt( ## ) );
               }
               os.exit( 0 );
            }
            case "-mklunemod" {
               let path = getNextOp();
               if! let mess = outputLuneMod( path ) {
                  Util.errorLog( mess );
                  os.exit( 1 );
               }
               os.exit( 0 );
            }
            case "-r" {
               option.useLuneModule = "lune.base._lune%d" (Ver.luaModVersion);
            }
            case "--runtime" {
               option.useLuneModule = getNextOp();
            }
            case "-oc" {
               option.bootPath = getNextOp();
            }
            case "-u" {
               option.updateOnLoad = true;
            }
            case "-Werror" {
               option.transCtrlInfo.stopByWarning = true;
            }
            case "--disable-checking-define-abbr" {
               option.transCtrlInfo.checkingDefineAbbr = false;
            }
            case "--log" {
               if! let txt = getNextOp() {
                  if! let level = Log.str2level( txt ) {
                     Log.setLevel( level );
                  }
                  else {
                     Util.errorLog( "illegal level -- %s" ( txt ) );
                  }
               }
            }
            case "--testing" {
               option.testing = true;
            }
            case "--depends" {
               option.dependsPath = getNextOp();
            }
            case "--uptodate" {
               if! let txt = getNextOp() {
                  if! let mode = CheckingUptodateMode._from( txt ) {
                     option.transCtrlInfo.uptodateMode = mode;
                  }
                  else {
                     Util.errorLog( "illegal mode -- " .. txt );
                  }
               }
            }
            case "-langC" {
               option.convertC = true;
            }
            case "-ol" {
               if! let txt = getNextOp() {
                  switch txt {
                     case "51" {
                        option.targetLuaVer = LuaVer.ver51;
                     }
                     case "52" {
                        option.targetLuaVer = LuaVer.ver52;
                     }
                     case "53" {
                        option.targetLuaVer = LuaVer.ver53;
                     }
                  }
               }
            }
            case "-ob0", "-ob1" {
               option.byteCompile = true;
               if arg == "-ob0" {
                  option.stripDebugInfo = true;
               }
            }
            default {
               Util.log( "unknown option -- %s" (arg) );
               os.exit( 1 );
            }
         }
      }
      else {
         if option.scriptPath == "" {
            option.scriptPath = arg;
         }
         elseif option.mode == "" {
            if! let mode = ModeKind._from( arg ) {
               option.mode = mode;
            }
            else {
               Util.err( "unknown mode -- %s" ( arg ) );
            }
         }
         else {
            switch( option.mode ) {
               case .Complete {
                  if not option.analyzeModule {
                     option.analyzeModule = arg;
                  }
                  elseif not lineNo {
                     lineNo = (unwrap tonumber( arg ## ) default 0)@@int;
                  }
                  elseif not column {
                     column = (unwrap tonumber( arg ## ) default 0)@@int;
                     option.analyzePos = new Parser.Position(
                        unwrap lineNo, unwrap column );
                  }
               }
               case .Save, .SaveMeta, .Glue {
                  option.outputDir = arg;
               }
               default {
               }
            }
         }
      }

      index = index + 1;
   }

   if option.scriptPath == "" or option.mode == .Unknown {
      printUsage( (#argList == 0 or argList[1] == "") and 0 or 1 );
   }

   if useStdInFlag and option.analyzeModule {
      Parser.StreamParser.setStdinStream( unwrap option.analyzeModule );
   }

   return option;
}
